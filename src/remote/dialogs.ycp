/**
 * File:	remote/dialogs.ycp
 * Module:	Network configuration
 * Summary:	Dialog for Remote Administration
 * Authors:	Arvin Schnell <arvin@suse.de>
 *
 * $Id$
 */

{

textdomain "network";

import "Label";
import "Remote";
import "Wizard";

global define symbol RemoteMainDialog() ``{

    ScreenName("remote");

    /* Ramote Administration dialog caption */
    string caption = _("Remote Administration");

    term allow_buttons = `RadioButtonGroup(`VBox(
	/* RadioButton label */
	`Left(`RadioButton(`id(`allow), _("&Allow Remote Administration"), false)),
	/* RadioButton label */
	`Left(`RadioButton(`id(`disallow), _("&Do not allow Remote Administration"), false))
    ));

    /* Ramote Administration dialog contents */
    term contents = `VBox(`HBox(
	`HStretch(),
	allow_buttons,
	`HStretch()
    ));

    /* Ramote Administration dialog help */
    string help = _("Here you can allow remote administration of the system.");

    Wizard::SetContentsButtons(caption, contents, help,
	    Label::BackButton(), Label::FinishButton());

    UI::ChangeWidget(`id(`allow), `Value, Remote::allow_administration);
    UI::ChangeWidget(`id(`disallow), `Value, !Remote::allow_administration);

    any ret = nil;

    repeat {

	ret = Wizard::UserInput();

	if(ret == `abort)
	{
	    break;
	}
	else if(ret == `help)
	{
	    Wizard::ShowHelp(help);
	}

    } until( ret == `next || ret == `back );

    if(ret == `next)
    {
	Remote::allow_administration = UI::QueryWidget(`id(`allow), `Value);
    }

    return ret;
}

/* EOF */
}
