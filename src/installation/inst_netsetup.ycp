/**
 * Module:	inst_netsetup.ycp
 *
 * Authors:	Arvin Schnell <arvin@suse.de>
 *
 * Purpose:	setup network in continue mode if required
 */
{
    symbol mode = WFM::Args (2);


    // TODO: handle dhcp case correctly


    if (mode == `up)
    {
	if (!Mode::test && !Arch::s390)
	{
	    SCR::Execute (.target.bash, "/sbin/ifconfig lo 127.0.0.1 up");

	    if ((Mode::boot == "nfs")
		|| (Mode::boot == "http")
		|| (Mode::boot == "smb")
		|| (Mode::boot == "ftp"))
	    {
		string netdev = SCR::Read (.etc.install_inf.Netdevice);
		if (netdev == nil) netdev = "eth0";

		string ipaddr = SCR::Read (.etc.install_inf.IP);
		if (ipaddr == nil) ipaddr = "";

		string ifconfig = "/sbin/ifconfig " + netdev + " up " + ipaddr;

		string netmask = SCR::Read (.etc.install_inf.Netmask);
		if (netmask != nil && netmask != "")
		{
		    ifconfig = ifconfig + " netmask " + netmask;
		}

		SCR::Execute (.target.bash, ifconfig);

		string gateway = SCR::Read (.etc.install_inf.Gateway);
		if (gateway != nil && gateway != "")
		{
		    ifconfig = "/sbin/route add default gw " + gateway;
		    SCR::Execute (.target.bash, ifconfig);
		}
	    }
	}

	if (Mode::boot == "nfs")
	{
	    SCR::Execute (.target.bash, "/sbin/rcportmap start");
	}
    }


    if (mode == `dn)
    {
	if (Mode::boot == "nfs")
	{
	    SCR::Execute (.target.bash, "/sbin/rcportmap stop");
	}

	if ((Mode::boot == "nfs")
	    || (Mode::boot == "http")
	    || (Mode::boot == "smb")
	    || (Mode::boot == "ftp"))
	{
	    string gateway = SCR::Read (.etc.install_inf.Gateway);
	    if (gateway != nil && gateway != "")
	    {
		ifconfig = "/sbin/route del default gw " + gateway;
		SCR::Execute (.target.bash, ifconfig);
	    }

	    string netdev = SCR::Read (.etc.install_inf.Netdevice);
	    if (netdev == nil) netdev = "eth0";

	    string ifconfig = "/sbin/ifconfig " + netdev + " down";
	    SCR::Execute (.target.bash, ifconfig);
	}
    }


    return `auto;
}
