/**
 * File:	include/network/lan/manual.ycp
 * Package:	Configuration of network
 * Summary:	Network card adresss configuration dialogs
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "network";

import "Arch";
import "Lan";
import "Wizard";

include "ui/common_popups.ycp";
include "ui/common_messages.ycp";

include "network/routines.ycp";
include "network/ip.ycp";

/**
 * Dialog for setting up IP address
 * @return dialog result
 */
global define symbol AddressDialog() ``{

    /* Address dialog caption */
    string caption = _("Network address setup");

    /* Address dialog help 1/8 */
    string helptext = _("<p>Configure your IP address.</p>") +

    /* Address dialog help 2/8 */
_("<p>You can select dynamic address assignment,
if you have a <b>DHCP server</b> running on your local network.</p>") +

    /* Address dialog help 3/8 */
_("<p>Also select this if you do not have a static
IP address assigned by the system administrator or your cable or DSL provider.
</p>") +

    /* Address dialog help 4/8 */
_("<p>Network addresses will then be obtained
<b>automatically</b> from the server.</p>") +

    /* Address dialog help 5/8 */
_("<p>Clicking <b>Next</b> 
completes the configuration.</p>") +

    /* Address dialog help 6/8 */
_("<p>Otherwise, network addresses must be assigned
<b>manually</b>.</p>") +

    /* Address dialog help 7/8 */
_("<p>Enter the IP address (e.g., 192.168.100.99) for
your computer, the network mask (usually 255.255.255.0), and, optionally, the
default gateway IP address.</p>") +

    /* Address dialog help 8/8 */
_("<p>Contact your <b>network administrator</b>
for more information about the network configuration.</p>");

// FIXME: better helps layout

    string bootproto = Lan::bootproto;
    string ipaddr = Lan::ipaddr;
    string remoteip = Lan::remoteip;
    string netmask = Lan::netmask;
    string type = Lan::type;
    string device = "";

    if(!Arch::s390 && bootproto == "static" && ipaddr == "")
	bootproto = "dhcp";

    if(ipaddr == "" && netmask == "")
	netmask = "255.255.255.0";

    if(Lan::device == -1)
	/* Label text (Network device: unknown device) */
	device = _("Unknown device");
    else
	device = type + Lan::device;

    term label = `HBox(
	/* Label text */
	`Label(_("Network device")),
	`HSpacing(0.5),
	// Network device: [unknown]
	`Label(`opt(`outputField), device)
	//`ComboBox(`id(`num), `opt(`hstretch,`editable), "", [ "eth1" ])
    );
    //label = `ComboBox(`id(`num), `opt(`hstretch,`editable), _("Network device"), [ "eth1" ]);

    /* Frame label */
    term frame1 = `Frame(_("Choose the setup method"),
	`HBox(`HSpacing(1),
	    `VBox(
		`VSpacing(0.5),
		/* Radio button label */
		`Left(`RadioButton(`id(`dynamic), `opt(`notify), _("A&utomatic address setup (via DHCP)"), bootproto != "static")),
		`VSpacing(0.5),
		/* Radio button label */
		`Left(`RadioButton(`id(`static), `opt(`notify), _("S&tatic address setup"), bootproto == "static")),
		`HBox(
		    /* Text entry label for IP address */
		    `TextEntry(`id(`ipaddr), _("&IP Address"), ipaddr),
		    `HSpacing(2),
		    /* Text entry label for network mask */
		    `TextEntry(`id(`netmask), _("&Subnet mask"), netmask)
		),
		`VSpacing(0.5)
	    ),
	`HSpacing(1))
    );

    if(Arch::s390) {

    /* Address dialog help 1/8 */
    helptext = _("<p>Configure your IP address.</p>");

    if(!Arch::s390)
    /* Address dialog help 2/8 */
    helptext = helptext + _("<p>You can select dynamic address assignment,
if you have a <b>DHCP server</b> running on your local network.</p>") +

    /* Address dialog help 3/8 */
_("<p>Also select this if you do not have a static
IP address assigned by the system administrator or your cable or DSL provider.
</p>") +

    /* Address dialog help 4/8 */
_("<p>Network addresses will then be obtained
<b>automatically</b> from the server.</p>") +

    /* Address dialog help 5/8 */
_("<p>Otherwise, network addresses must be assigned
<b>manually</b>.</p>");

    if(type == "ctc" || type == "escon" || type == "iucv")
    /* Address dialog help 6/8 */
helptext = helptext + _("<p>Enter the <b>IP address</b> (e.g., 192.168.100.99)
for your computer and the <b>remote IP address</b> (e.g., 192.168.100.254)
of your peer.</p>");

    else
    /* Address dialog help 6/8 */
    helptext = helptext + _("<p>Enter the IP address (e.g., 192.168.100.99) for
your computer, the network mask (usually 255.255.255.0), and, optionally, the
default gateway IP address.</p>");

    /* Address dialog help 7/8 */
    helptext = helptext + _("<p>Contact your <b>network administrator</b>
for more information about the network configuration.</p>") +

    /* Address dialog help 8/8 */
_("<p>Clicking <b>Next</b> 
completes the configuration.</p>");

    /* s390 help hackery */
    }

    /* S/390 Hipersockets */
    if(type == "hsi" || Arch::s390)

	/* Frame label */
	frame1 = `Frame(_("Static address setup"),
	    `VBox(
		`VSpacing(0.5),
		`HBox(
		    /* Text entry label */
		    // FIXME: Address -> address
		    `TextEntry(`id(`ipaddr), _("&IP Address"), ipaddr),
		    `HSpacing(2),
		    /* Text entry label */
		    `TextEntry(`id(`netmask), _("&Subnet mask"), netmask)
		),
		`VSpacing(0.5)
	    ));


    /* S/390 CTC, ESCON and IUCV */
    if(type == "ctc" || type == "escon" || type == "iucv")

	/* Frame label */
	frame1 = `Frame(_("Static address setup"),
	    `VBox(
		`VSpacing(0.5),
		`HBox(
		    /* Text entry label */
		    `TextEntry(`id(`ipaddr), _("&IP Address"), ipaddr),
		    // FIXME: Address -> address
		    `HSpacing(2),
		    /* Text entry label */
		    `TextEntry(`id(`remoteip), _("R&emote IP address"), remoteip)
		),
		`VSpacing(0.5)
	    ));

    /* Frame label */
    term frame2 = `Frame(_("Detailed settings"), `HBox(`HStretch(),
	`HSquash(`VBox(
	    `VSpacing(0.5),
	    /* Push button label */
	    `PushButton(`id(`hostname), `opt(`hstretch), _("&Host name and name server")),
	    /* Push button label */
	    `PushButton(`id(`routing), `opt(`hstretch), _("R&outing")),
	    `VSpacing(0.5)
	)),
	`HStretch()
    ));

    /* Address dialog help */
    term contents = `RadioButtonGroup(`id(`rb), `HSquash(`VBox(
	`Left(label),
	`VSpacing(1),
	frame1,
	`VSpacing(1),
	frame2
    )));

    Wizard::SetContentsButtons(caption, contents, helptext,
	    BackButtonLabel(), NextButtonLabel());

    UI::ChangeWidget(`id(`ipaddr), `Enabled, bootproto=="static");
    ChangeWidgetIfExists(`id(`netmask), `Enabled, bootproto=="static");
    ChangeWidgetIfExists(`id(`dynamic), `Value, bootproto!="static");
    ChangeWidgetIfExists(`id(`static), `Value, bootproto=="static");

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
	/* static ip address */
	else if(ret == `dynamic) {
	    UI::ChangeWidget(`id(`ipaddr), `Enabled, false);
	    ChangeWidgetIfExists(`id(`netmask), `Enabled, false);
	    ChangeWidgetIfExists(`id(`remoteip), `Enabled, false);
	    continue;
	}
	/* dynamic ip address via dhcp */
	else if(ret == `static) {
	    UI::ChangeWidget(`id(`ipaddr), `Enabled, true);
	    ChangeWidgetIfExists(`id(`netmask), `Enabled, true);
	    ChangeWidgetIfExists(`id(`remoteip), `Enabled, true);
	    continue;
	}
	/* netmask from bits to IP mapping */
	else if(ret == `netmask) {
	    string nm = UI::QueryWidget(`id(`netmask), `Value);
	    if(regexpmatch(nm, "^[0-9]+$"))
		UI::ChangeWidget(`id(`netmask), `Value, bits2netmask(tointeger(nm)));
	    continue;
	}
	/* back */
	else if(ret == `back) {
	    break;
	}
	/* FIXME: better hostname and routing workflow */
	else if(ret == `next || ret == `routing || ret == `hostname) {
	    if(UI::QueryWidget(`id(`rb), `CurrentButton) != `dynamic) {
		if(!check_ip4(UI::QueryWidget(`id(`ipaddr), `Value))) {
		    /* Popup text */
		    UI::ErrorPopup(_("The IP address is incorrect"));
		    UI::SetFocus(`id(`ipaddr));
		    continue;
		}
		if(UI::WidgetExists(`id(`remoteip)) && !check_ip4(UI::QueryWidget(`id(`remoteip), `Value))) {
		    /* Popup text */
		    UI::ErrorPopup(_("The remote IP address is incorrect"));
		    UI::SetFocus(`id(`remoteip));
		    continue;
		}
		if(UI::WidgetExists(`id(`netmask)) && !check_netmask(UI::QueryWidget(`id(`netmask), `Value))) {
		    /* Popup text */
		    UI::ErrorPopup(_("The subnet mask is incorrect"));
		    UI::SetFocus(`id(`netmask));
		    continue;
		}
	    }
	    break;
	}
	else {
	    y2error("unexpected retcode: %1", ret);
	    continue;
	}

    }

    /* update the configuration */
    if(ret == `next || ret == `routing || ret == `hostname) {
	Lan::ipaddr = UI::QueryWidget(`id(`ipaddr), `Value);

	if(UI::WidgetExists(`id(`netmask)))
	    Lan::netmask = UI::QueryWidget(`id(`netmask), `Value);

	if(UI::WidgetExists(`id(`rb))) {
	    if(UI::QueryWidget(`id(`rb), `CurrentButton) == `dynamic)
		Lan::bootproto = "dhcp";
	    else
		Lan::bootproto = "static";
	}

	if(UI::WidgetExists(`id(`remoteip)))
	    Lan::remoteip = UI::QueryWidget(`id(`remoteip), `Value);
    }

    return ret;
}

/* EOF */
}
