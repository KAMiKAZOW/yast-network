/**
 * File:	include/network/lan/wizards.ycp
 * Package:	Network configuration
 * Summary:	Network cards configuration wizards
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "network";

import "Label";
import "Lan";
import "Wizard";

include "wizard/sequencer.ycp";

include "network/routines.ycp";
include "network/lan/address.ycp";
include "network/lan/complex.ycp";
include "network/lan/hardware.ycp";
include "network/services/dns.ycp";
include "network/services/routing.ycp";

/**
 * Whole configuration of network
 * @return sequence result
 */
global define symbol LanSequence() ``{

    /* Popup text */
    string finished = _("Configure the mail now?");

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ],
	"finish" : [ ``( Finished(Lan::Modified(), finished, "mail", ["permanent"]) ), true ],
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next,
	],
	"finish" : $[
	    `next	: `next,
	]
    ];

    string caption = _("Network cards configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
				Label::BackButton(), Label::NextButton());

    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of network but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol LanAutoSequence() ``{
    string caption = _("Network cards configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
				Label::BackButton(), Label::NextButton());

    symbol ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the network configuration
 * @return sequence result
 */
global define symbol MainSequence() ``{

    map aliases = $[
	"summary"	: ``(SummaryDialog()),
	"overview"	: ``(OverviewDialog()),
	"configure"	: [ ``(NetworkCardSequence("edit")), true ],
	"other"		: [ ``(NetworkCardSequence("add")), true ],
	"add"		: [ ``(NetworkCardSequence("add")), true ],
	"edit"		: [ ``(NetworkCardSequence("edit")), true ],
	"commit"	: [ ``(Commit()), true ],
    ];

    map sequence = $[
	"ws_start" : "summary",
	"summary" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `configure	: "configure",
	    `other	: "other",
	    `overview	: "overview"
	],
	"overview" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add",
	    `edit	: "edit",
	],
	"other" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"configure" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"commit" : $[
	    `next	: "summary",
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/**
 * Configuration sequence
 * @param action which workflow ("add"|"edit")
 * @return sequence result
 */
global define symbol NetworkCardSequence(string action) ``{

    map aliases = $[
	"hardware"	: ``(HardwareSequence()),
	"address"	: ``(AddressSequence()),
    ];

    string ws_start = "hardware";
    if(action == "edit") ws_start = "address";

    map sequence = $[
	"ws_start" : ws_start,
	"hardware" : $[
	    `abort	: `abort,
	    `next	: "address",
	],
	"address" : $[
	    `abort	: `abort,
	    `hardware	: "hardware",
	    `next	: `next,
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/**
 * Configuration sequence
 * @return sequence result
 */
global define symbol HardwareSequence() ``{

    map aliases = $[
	"hardware"	: ``(HardwareDialog()),
	"list"		: ``(SelectionDialog()),
	"wire"		: ``(WirelessDialog()),
	"s390"		: ``(S390Dialog()),
    ];

    map sequence = $[
	"ws_start"	: "hardware",
	"hardware" : $[
	    `abort	: `abort,
	    `list	: "list",
	    `wire	: "wire",
	    `s390	: "s390",
	    `next	: `next,
	],
	"wire" : $[
	    `next	: "hardware",
	    `abort	: `abort,
	],
	"s390" : $[
	    `next	: "hardware",
	    `abort	: `abort,
	],
	"list" : $[
	    `next	: "hardware",
	    `abort	: `abort,
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/**
 * Configuration sequence
 * @return sequence result
 */
global define symbol AddressSequence() ``{

    map aliases = $[
	"address"	: ``(AddressDialog()),
	"hostname"	: ``(DNSMainDialog(false)),
	"routing"	: ``(RoutingMainDialog(false)),
	"commit"	: [ ``(Commit()), true ],
    ];

    map sequence = $[
	"ws_start"	: "address",
	"address" : $[
	    `abort	: `abort,
	    `next	: "commit",
	    `routing	: "routing",
	    `hostname	: "hostname",
	    `hardware	: `hardware,
	],
	"routing" : $[
	    `abort	: `abort,
	    `next	: "address",
	],
	"hostname" : $[
	    `abort	: `abort,
	    `next	: "address",
	],
	"commit" : $[
	    `next	: `next,
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/* EOF */
}
