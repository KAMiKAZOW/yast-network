/**
 * File:
 *   include/network/lan/wizards.ycp
 *
 * Package:
 *   Configuration of network cards
 *
 * Summary:
 *   User interface functions.
 *
 * Authors:
 *   Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * Wizards definitions.
 *
 */

{

textdomain "network";

import "Wizard";

include "wizard/sequencer.ycp";

include "network/lan/manual.ycp";
include "network/lan/address.ycp";
include "network/lan/dialogs.ycp";

/**
 * Whole configuration of network
 * @return sequence result
 */
global define symbol LanSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" :
	$[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" :
	$[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    string caption = _("Network cards configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
				BackButtonLabel(), NextButtonLabel());

    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of network but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol LanAutoSequence() ``{
    string caption = _("Network cards configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
				BackButtonLabel(), NextButtonLabel());

    symbol ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the network configuration
 * @return sequence result
 */
global define symbol MainSequence() ``{

    map aliases = $[
	"summary"	: ``(SummaryDialog()),
	"overview"	: ``(OverviewDialog()),
	"configure"	: [ ``(AddressSequence()), true ],
	"other"		: [ ``(ManualSequence()), true ],
	"add"		: [ ``(ManualSequence()), true ],
	"edit"		: [ ``(AddressSequence()), true ],
	"delete"	: ``(DeleteDialog()),
	"commit"	: [ ``(Commit()), true ],
    ];

    map sequence = $[
	"ws_start" : "summary",
	"summary" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `configure	: "configure",
	    `other	: "other",
	    `overview	: "overview"
	],
	"overview" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add",
	    `edit	: "edit",
	    `delete	: "delete"
	],
	"other" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"configure" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"commit" : $[
	    `next	: "summary",
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"delete" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/**
 * Configuration sequence
 * @return sequence result
 */
global define symbol ManualSequence() ``{

    map aliases = $[
	"manual"	: ``(ManualDialog()),
	"address"	: ``(AddressDialog()),
	"list"		: ``(ManualSelection()),
	"commit"	: [ ``(Commit()), true ],
    ];

    map sequence = $[
	"ws_start"	: "manual",
	"manual" : $[
	    `abort	: `abort,
	    `list	: "list",
	    `next	: "address",
	],
	"list" : $[
	    `next	: "manual",
	    `abort	: `abort,
	],
	"address" : $[
	    `abort	: `abort,
	    `next	: "commit",
	],
	"commit" : $[
	    `next	: `next,
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/**
 * Configuration sequence
 * @return sequence result
 */
global define symbol AddressSequence() ``{

    map aliases = $[
	"address"	: ``(AddressDialog()),
	"commit"	: [ ``(Commit()), true ],
    ];

    map sequence = $[
	"ws_start"	: "address",
	"address" : $[
	    `abort	: `abort,
	    `next	: "commit",
	],
	"commit" : $[
	    `next	: `next,
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/* EOF */
}
