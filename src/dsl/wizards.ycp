/**
 * File:	include/network/dsl/wizards.ycp
 * Package:	Network configuration
 * Summary:	DSL configuration wizards
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "network";

import "Label";
import "Wizard";

include "wizard/sequencer.ycp";

include "network/routines.ycp";
include "network/provider/wizards.ycp";
include "network/dsl/dialogs.ycp";
include "network/dsl/complex.ycp";

/**
 * Whole configuration of DSL
 * @return sequence result
 */
global define symbol DSLSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( DSLMainSequence() ),
	"rpm"	: [ ``( Packages(["smpppd"]) ), true ],
	"write"	: [ ``( WriteDialog() ), true ],
	"finish": [ ``( FinishDialog() ), true ],
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "rpm"
	],
	"rpm" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: "finish"
	],
	"finish" : $[
	    `next	: `next,
	]
    ];

    Wizard::CreateDialog();

    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of DSL but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol DSLAutoSequence() ``{
    string caption = _("DSL configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    symbol ret = DSLMainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the DSL configuration
 * @return sequence result
 */
global define symbol DSLMainSequence() ``{

    map aliases = $[
	"summary"	: ``(SummaryDialog()),
	"overview"	: ``(OverviewDialog()),
	"configure"	: [ ``(OneDSLProviderSequence(true,  false)), true ],
	"other"		: [ ``(OneDSLProviderSequence(false, false)), true ],
	"add"		: [ ``(OneDSLProviderSequence(false, false)), true ],
	"edit"		: [ ``(OneDSLProviderSequence(false, true)), true ],
	// "edit"		: [ ``(OneDSLSequence(false)), true ],
	"Add"		: [ ``(OneProviderSequence(true)), true ],
	"Edit"		: [ ``(OneProviderSequence(false)), true ],
    ];

    map sequence = $[
	"ws_start" : "summary",
	"summary" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `configure	: "configure",
	    `other	: "other",
	    `overview	: "overview"
	],
	"overview" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add",
	    `edit	: "edit",
	    `Add	: "Add",
	    `Edit	: "Edit",
	],
	"other" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"configure" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"Add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"Edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/**
 * Workflow of the configuration of one DSL
 * @param detected true if DSL was detected (more entries otherwise)
 * @param edit true in case of edit sequence
 * @return sequence result
 */
global define symbol OneDSLProviderSequence(boolean detected, boolean edit) ``{

    map aliases = $[
	"parameters_detected"	: ``(DSLDialog()),
	"parameters"		: ``(DSLDialog()),
	"details"		: ``(DSLDetailsDialog()),
	"details_detected"	: ``(DSLDetailsDialog()),
	"providers"		: ``(ProvidersDialog(edit)),
	"provider"		: ``(ProviderDialog()),
	"new_provider"		: ``(ProviderDialog()),
	"connection"		: ``(ConnectionDialog()),
	"ipdetails"		: ``(IPDetailsDialog()),
	"commit"		: [ ``(Commit("")), true ],
    ];

    string entry = "parameters";
    if(detected) entry = "parameters_detected";

    map sequence = $[
	"ws_start" : entry,
	"parameters": $[
	    `next   : "providers",
	    `Details: "details",
	    `abort  : `abort
	],
	"parameters_detected" : $[
	    `next   : "providers",
	    `Details: "details_detected",
	    `abort  : `abort
	],
	"details"   : $[
	    `next   : "parameters",
	    `abort  : `abort
	],
	"details_detected" : $[
	    `next   : "parameters_detected",
	    `abort  : `abort
	],
	"providers" : $[
	    `next   : "provider",
	    `new    : "new_provider",
	    `abort  : `abort
	],
	"provider" : $[
	    `next   : "connection",
	    `abort  : `abort
	],
	"new_provider" : $[
	    `next   : "connection",
	    `abort  : `abort
	],
	"connection" : $[
	    `IPDetails   : "ipdetails",
	    `next   : "commit",
	    `abort  : `abort
	],
	"ipdetails" : $[
	    `next   : "connection",
	    `abort  : `abort
	],
	"commit" : $[
	    `next   : `next,
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/**
 * Workflow of the configuration of one DSL
 * @param detected true if DSL was detected (more entries otherwise)
 * @return sequence result
 */
global define symbol OneDSLSequence(boolean detected) ``{

    map aliases = $[
	"parameters_detected"	: ``(DSLDialog()),
	"parameters"		: ``(DSLDialog()),
	"details"		: ``(DSLDetailsDialog()),
	"details_detected"	: ``(DSLDetailsDialog()),
	"commit"		: [ ``(Commit("dsl")), true ],
    ];

    string entry = "parameters";
    if(detected) entry = "parameters_detected";

    map sequence = $[
	"ws_start" : entry,
	"parameters": $[
	    `next   : "commit",
	    `Details: "details",
	    `abort  : `abort
	],
	"parameters_detected" : $[
	    `next   : "commit",
	    `Details: "details_detected",
	    `abort  : `abort
	],
	"details"   : $[
	    `next   : "parameters",
	    `abort  : `abort
	],
	"details_detected" : $[
	    `next   : "parameters_detected",
	    `abort  : `abort
	],
	"commit" : $[
	    `next   : `next,
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/* EOF */
}
