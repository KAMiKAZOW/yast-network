/**
 * File:	include/network/routines.ycp
 * Package:	Network configuration
 * Summary:	Miscellaneous routines
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "network";

import "Call";
import "Popup";
import "Progress";
import "String";

/**
 * Abort function
 * @return blah blah lahjk
 */
define boolean Abort() {
    import "Mode";
    if(Mode::commandline) return false;
    return UI::PollInput() == `abort;

    /* FIXME: NI
    if(AbortFunction != nil)
	return eval(AbortFunction) == true;
    */
    return false;
}

/**
 * Check for pending Abort press
 * @return true if pending abort
 */
define boolean PollAbort() {
    return UI::PollInput() == `abort;
}

/**
 * If modified, ask for confirmation
 * @return true if abort is confirmed
 */
define boolean ReallyAbort() {
    return Popup::ReallyAbort(true);

    /* FIXME: NI
    return !Modified() || Popup::ReallyAbort(true);
    */
}

/**
 * If modified, ask for confirmation
 * @param modified true if modified
 * @return true if abort is confirmed
 */
define boolean ReallyAbortCond(boolean modified) {
    return (!modified) || Popup::ReallyAbort(true);

    /* FIXME: NI
    return (!modified && !Modified()) || Popup::ReallyAbort(true);
    */
}

/**
 * Set screen shot name
 * @param name screenshot name
 */
define void ScreenName(string name) {
    if(name != "" && name != nil)
	UI::SetModulename(/* "network/" + */ name);
}

/**
 * Progress::NextStage and Progress::Title combined into one function
 * @param title progressbar title
 */
define void ProgressNextStage(string title) {
    Progress::NextStage();
    Progress::Title(title);
}

/**
 * Change UI widget only if it exists
 * @param id widget id
 * @param param widget parameter
 * @param value widget parameter value
 */
define void ChangeWidgetIfExists(term id, symbol param, any value) {
    if(UI::WidgetExists(id))
	UI::ChangeWidget(id, param, value);
    else
	y2debug("Not changing: %1", id);
}

/**
 * Query UI widget only if it exists
 * @param id widget id
 * @param param widget parameter
 * @param value previous parameter value
 * @return widget value if exists, previous value otherwise
 */
define any QueryWidgetIfExists(term id, symbol param, any value) {
    if(UI::WidgetExists(id))
	return UI::QueryWidget(id, param);
    y2debug("Not changing: %1", id);
    return value;
}

/**
 * Check if required packages are installed and install them if they're not
 * @param packages list of required packages (["rpm", "bash"])
 * @return `next if packages installation is successfull, `abort otherwise
 */
define symbol PackagesInstall(list<string> packages) {
    if(size(packages) < 1) return `next;
    y2debug("Checking packages: %1", packages);

    import "Require";
    if(Require::AreAllPackagesInstalled(packages)) return `next;

    /* Popup text */
    string text = _("These packages need to be installed:") + "<p>";
    foreach(string l, packages, { text = text + sformat("%1<br>",l); });
    y2debug("Installing packages: %1", text);

    boolean ret = false;
    while(true) {
	ret = Require::RequireAndConflict(packages, [], text);
	if(ret == true) break;

	if(ret == false && Require::AreAllPackagesInstalled(packages)) {
	    ret = true;
	    break;
	}

	/* Popup text */
	if(!Popup::YesNo(_("The required packages are not installed.
The configuration will be aborted.

Try again?
") + "
"))
	    break;
    }

    return ret == true ? `next : `abort;
}

/**
 * Create comment for changed file
 * @param modul YaST2 module changing the file
 * @return comment
 * @example ChangedComment("lan") -> # Changed by YaST2 module lan 1.1.2000"
 */
define string ChangedComment(string modul) {
    string ret = "\n# Changed by YaST2";
    if(modul != nil && modul != "") ret = ret + " module " + modul;
    string date = lookup((map)SCR::Execute(.target.bash_output, "/bin/date '+%x'"), "stdout", "");
    if(date != "") ret = ret + " " + date;
    return ret;
}

/**
 * Show busy popup (for proposal)
 * @param message label to bw shown
 */
define void BusyPopup(string message) {
    UI::BusyCursor();
    UI::OpenDialog(`VBox(`Label(message)));
}

/**
 * Close busy popup
 * @see BusyPopup
 */
define void BusyPopupClose() {
    UI::CloseDialog();
}

/**
 * Create a list of items for UI from the given list
 * @param l given list for conversion
 * @param selected selected item (0 for the first)
 * @return a list of items
 * @example [ "x", "y" ] -&gt; [ `item(`id(0), "x"), `item(`id(1), "y") ]
 */
define list list2items(list l, integer selected) {
    list items = [];
    integer n = 0;
    foreach(any i, l, {
	items = add(items, `item(`id(n), i, n == selected));
	n = n + 1;
    });
    return items;
}

/**
 * Create a list of items for UI from the given hardware list
 * @param l given list for conversion
 * @param selected selected item (0 for the first)
 * @return a list of items
 */
define list hwlist2items(list<map> l, integer selected) {
    list items = [];
    integer n = 0;
    foreach(map i, l, {
	/* Table field (Unknown device) */
	string hwname = i["name"]:_("Unknown");
	integer num = i["num"]:n;
	items = add(items, `item(`id(num), hwname, num == selected));
	n = n + 1;
    });
    return items;
    //return list2items(maplist(map h, l, { return h["name"]:_("Unknown device"); }), selected);
}

/**
 * Return a device type
 * @param dev device
 * @return device type
 * @example device_type("eth1") -> "eth"
 * @example device_type("eth-pcmcia-0") -> "eth-pcmcia"
 */
define string device_type(string dev) {
    if(regexpmatch(dev, "^([a-zA-Z]+)-?[^a-zA-Z]*") != true) return "";
    string ret = regexpsub(dev, "^([a-zA-Z]+)-?[^a-zA-Z]*", "\\1");
    /* FIXME: devname
    if(regexpmatch(dev, "^([a-zA-Z-]*[a-zA-Z])[^a-zA-Z]*") != true) return "";
    string ret = regexpsub(dev, "^([a-zA-Z-]*[a-zA-Z])[^a-zA-Z]*", "\\1");
    */
    if(ret == nil) ret = "";
    return ret;
}

/**
 * Return a device number
 * @param dev device
 * @return device number
 * @example device_num("eth1") -> "1"
 * @example device_num("lo") -> ""
 */
define string device_num(string dev) {
    if(regexpmatch(dev, "^[a-zA-Z]+-?([^#]*)") != true) return "";
    string ret = regexpsub(dev, "^[a-zA-Z]+-?([^#]*)", "\\1");
    /* FIXME: devname
    if(regexpmatch(dev, "[a-zA-Z-]*([0-9]+)") != true) return "";
    string ret = regexpsub(dev, "[a-zA-Z-]*([0-9]+)", "\\1");
    */
    if(ret == nil) ret = "";
    return ret;
}

/**
 * Return a device alias number
 * @param dev device
 * @return alias number
 * @example alias_num("eth1#2") -> "2"
 * @example alias_num("eth1#blah") -> "blah"
 */
define string alias_num(string dev) {
    // FIXME: devname
    // return "";
    if(regexpmatch(dev, "[a-zA-Z-]*[0-9]*#([a-zA-Z0-9_-]+)") != true) return "";
    string ret = regexpsub(dev, "[a-zA-Z-]*[0-9]*#([a-zA-Z0-9_-]+)", "\\1");
    if(ret == nil) ret = "";
    return ret;
}

/**
 * Test hotplugability of a device
 * @param type device type
 * @return true if hotpluggable
 */
define boolean IsHotplug(string type) {
    if(type == "" || type == nil) return false;
    if(regexpmatch(type, "(pcmcia|usb|pci)$")) return true;
    return false;
}

/**
 * Create a device name from its type and number
 * @param typ device type
 * @param num device number
 * @return device name
 * @example device_name("eth", "1") -> "eth1"
 * @example device_name("lo", "") -> "lo"
 */
define string device_name(string typ, string num) {
    if(typ == nil || typ == "") {
	y2error("wrong type: %1", typ);
	return nil;
    }
    if(num == nil /* || num < 0 */) {
	y2error("wrong number: %1", num);
	return nil;
    }
    /* FIXME: devname
    if(IsHotplug(typ) && num != "") return sformat("%1-%2", typ, num);
    return sformat("%1%2", typ, num);
    */
    if(regexpmatch(num, "^[0-9]*$"))
	return sformat("%1%2", typ, num);
    return sformat("%1-%2", typ, num);
}

/**
 * Create a alias name from its type and numbers
 * @param typ device type
 * @param num device number
 * @param anum alias number
 * @return alias name
 * @example alias_name("eth", "1", "2") -> "eth1#2"
 */
define string alias_name(string typ, string num, string anum) {
    if(typ == nil || typ == "") {
	y2error("wrong type: %1", typ);
	return nil;
    }
    if(num == nil /* || num < 0 */) {
	y2error("wrong number: %1", num);
	return nil;
    }
    if(anum == nil || anum == "") {
	y2error("wrong alias number: %1", anum);
	return nil;
    }
    return sformat("%1#%2", device_name(typ, num), anum);
}

/**
 * Return real type of the device (incl. PCMCIA, USB, ...)
 * @param type basic device type
 * @param hotplug hot plug type
 * @return real type
 * @example RealType("eth", "usb") -> "eth-usb"
 */
define string RealType(string type, string hotplug) {

    y2debug("type=%1", type);
    if(type == "" || type == nil) {
	y2error("Wrong type: %1", type);
	return "eth";
    }

    if(hotplug == "" || hotplug == nil)
	return type;

    string realtype = type + "-" + hotplug;
    y2debug("realtype=%1", realtype);
    return realtype;
}

/**
 * Display the finished popup and possibly run another module.
 * If not modified, don't do anything.
 * @param modified true if there are any modified data
 * @param head headline to be shown
 * @param text text to be shown
 * @param run module to be run
 * @param params parameters to pass to the module
 * @return always `next
 */
define symbol FinishPopup(boolean modified, string head, string text, string run, list params) {

    if(!modified) return `next;

    string h = head;
    if(h == nil || h == "")
	/* Popup headline */
	h = _("Configuration has been successfully saved.");

    map heads = $[
	/* Popup headline */
	"dns"		: _("DNS Configuration Successfully Saved"),
	/* Popup headline */
	"dsl"		: _("DSL Configuration Successfully Saved"),
	/* Popup headline */
	"host"		: _("Hosts Configuration Successfully Saved"),
	/* Popup headline */
	"isdn"		: _("ISDN Configuration Successfully Saved"),
	/* Popup headline */
	"lan"		: _("Network Card Configuration Successfully Saved"),
	/* Popup headline */
	"modem"		: _("Modem Configuration Successfully Saved"),
	/* Popup headline */
	"proxy"		: _("Proxy Configuration Successfully Saved"),
	/* Popup headline */
	"provider"	: _("Provider Configuration Successfully Saved"),
	/* Popup headline */
	"routing"	: _("Routing Configuration Successfully Saved"),
    ];
    h = heads[head]:h;

    string t = text;
    map texts = $[
	/* Popup text */
	"mail"	: _("Configure mail now?"),
    ];
    if(t == "")
	t = texts[run]:text;
    if(t == "")
	/* Popup text */
	t = sformat(_("Run configuration of %1?"), run);

    any ret = nil;
    if(run != "") {
	ret = Popup::YesNoHeadline(h, t);
	/* FIXME: check for the module presence */
	if(ret == true) Call::Function(run, params);
    }
    else
	ret = Popup::AnyMessage(h, t);

    return `next;
}

/* EOF */
}
