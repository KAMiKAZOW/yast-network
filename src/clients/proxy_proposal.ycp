/**
 * File:	clients/proxy_proposal.ycp
 * Package:	Configuration of proxy
 * Summary:	Proposal function dispatcher.
 * Authors:	Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 *
 * Proposal function dispatcher for proxy configuration.
 * See source/installation/proposal/proposal-API.txt
 */

{

textdomain "network";

import "Proxy";
import "Progress";

/* The main () */
y2milestone("----------------------------------------");
y2milestone("Proxy proposal started");

string func = WFM::Args(0);
map param = WFM::Args(1);
map ret = $[];

/* create a textual proposal */
if(func == "MakeProposal") {
    string proposal = "";
    string warning = nil;
    symbol warning_level = nil;
    boolean force_reset = param["force_reset"]:false;

    if(force_reset || !Proxy::proposal_valid) {
	Proxy::proposal_valid = true;
	Progress::off();
	Proxy::Read();
    }
    Proxy::enabled = false;
    proposal = Proxy::Summary();
    Progress::on();

    ret = $[
	"preformatted_proposal" : proposal,
	"warning_level" : warning_level,
	"warning" : warning,
    ];
}
/* run the module */
else if(func == "AskUser") {
    map stored = Proxy::Export();
    symbol seq = WFM::CallFunction(`proxy(.propose));
    if(seq != `next) Proxy::Import(stored);
    y2debug("stored=%1",stored);
    y2debug("seq=%1",seq);
    ret = $[
	"workflow_sequence" : seq
    ];
}
/* create titles */
else if(func == "Description") {
    ret = $[
	"rich_text_title" : _("Proxy"),
	"menu_title" : _("&Proxy"),
	"id" : "proxy",
    ];
}
/* write the proposal */
else if(func == "Write") {
    Proxy::Write();
}
/* unknown function */
else {
    y2error("unknown function: %1", func);
}

/* Finish */
y2debug("ret=%1",ret);
y2milestone("Proxy proposal finished");
y2milestone("----------------------------------------");
return ret;

/* EOF */
}
