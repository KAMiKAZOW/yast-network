/**
 * File:	clients/proxy_auto.ycp
 * Package:	Network configuration
 * Summary:	Client for autoinstallation
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param function to execute
 * @param map/list of proxy settings
 * @return map edited settings, Summary or boolean on success depending on called function
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = WFM::CallFunction ("proxy_auto", [ "Summary", mm ]);
 */

{

textdomain "network";

y2milestone("----------------------------------------");
y2milestone("Proxy auto started");

import "Proxy";
import "Wizard";

include "network/services/proxy.ycp";

/**
 * Return a modification status
 * @return true if data was modified
 */
global define boolean Modified() ``{
    return Proxy::modified;
}


any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(Args()) > 0 && is(Args(0), string)) {
    func = WFM::Args(0);
    if(size(Args()) > 1 && is(Args(1), map))
	param = WFM::Args(1);
}
y2debug("func=%1", func);
y2debug("param=%1", param);

/* Create a summary*/
if(func == "Summary") {
    ret = Proxy::Summary();
}
/* Reset configuration */
else if (func == "Reset") {
    Proxy::Import($[]);
    ret = $[];
}
/* Change configuration (run AutoSequence) */
else if (func == "Change") {
    Wizard::CreateDialog();
    ret = ProxyMainDialog(true);
    UI::CloseDialog();
}
/* return required package list */
else if (func == "Packages") {
    ret = $[];
}
/* Import configuration */
else if (func == "Import") {
    ret = Proxy::Import(param);
}
/* Return actual state */
else if (func == "Export") {
    ret = Proxy::Export();
}
/* Write givven settings */
else if (func == "Write") {
    import "Progress";
    Progress::off();
    Proxy::write_only = true;
    ret = Proxy::Write();
    Progress::on();
}
/* Unknown function */
else {
    y2error("Unknown function: %1", func);
    ret = false;
}

y2debug("ret=%1", ret);
y2milestone("Proxy auto finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
