/**
 * File:	clients/host_auto.ycp
 * Package:	Network configuration
 * Summary:	Client for autoinstallation
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param first a map of host settings
 * @return boolean success of operation
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = WFM::CallFunction("host_auto", [ mm ]);
 */

{

textdomain "network";

y2milestone("----------------------------------------");
y2milestone("Host auto started");

import "Host";
import "Label";
import "Wizard";

include "network/services/host.ycp";

/**
 * Return a modification status
 * @return true if data was modified
 */
global define boolean Modified() ``{
    return Host::modified;
}

any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(Args()) > 0 && is(Args(0), string)) {
    func = WFM::Args(0);
    if(size(Args()) > 1 && is(Args(1), map))
	param = WFM::Args(1);
}
y2debug("func=%1", func);
y2debug("param=%1", param);

/* Create a summary*/
if(func == "Summary") {
    ret = Host::Summary();
}
/* Reset configuration */
else if (func == "Reset") {
    Host::Import($[]);
    ret = $[];
}
/* Change configuration (run AutoSequence) */
else if (func == "Change") {
    Wizard::CreateDialog();
    Wizard::ReplaceNextButton(`PushButton(`id(`next), `opt(`default, `key_F10), Label::FinishButton()));
    ret = HostsMainDialog(false);
    Wizard::CloseDialog();
}
/* Import configuration */
else if (func == "Import") {
    list hosts = param["hosts"]:[];
    map hostlist = listmap(map host, hosts, ``{
	    return ([ host["host_address"]:"error", host["names"]:[] ] );
    });
    ret = Host::Import($["hosts": hostlist]);
}
/* Return actual state */
else if (func == "Export") {
    map ret1 = Host::Export();
    map hosts = ret1["hosts"]:$[];
    list ret2 = maplist (string hostaddress, list names, hosts, ``{
	    return($["host_address": hostaddress, "names": names]);
    });
    map result = $["hosts" : ret2 ];
    ret = result;
}
/* Read current state */
else if (func == "Read") {
    import "Progress";
    Progress::off();
    ret = Host::Read();
    Progress::on();
}
else if (func == "Packages") {
    ret = $[];
}
/* Write givven settings */
else if (func == "Write") {
    import "Progress";
    Progress::off();
    Host::write_only = true;
    ret = Host::Write();
    Progress::on();
}
/* Unknown function */
else {
    y2error("Unknown function: %1", func);
    ret = false;
}

y2debug("ret=%1", ret);
y2milestone("Host auto finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */

}
