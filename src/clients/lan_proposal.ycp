/**
 * File:	clients/lan_proposal.ycp
 * Package:	Configuration of network
 * Summary:	Lan configuration proposal
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "network";

import "Lan";
import "Arch";
import "Progress";

/* The main () */
y2milestone("----------------------------------------");
y2milestone("Lan proposal started");

string func = WFM::Args(0);
map param = WFM::Args(1);
map ret = $[];

/* create a textual proposal */
if(func == "MakeProposal") {
    string proposal = "";
    string warning = nil;
    symbol warning_level = nil;
    boolean force_reset = param["force_reset"]:false;

    if(force_reset || !Lan::proposal_valid) {
	Lan::proposal_valid = true;
	Progress::off();
	Lan::Read();
    }
    Lan::Propose();
    list sum = Lan::Summary(false);
    proposal = select(sum, 0, "");
    Progress::on();

    ret = $[
	"preformatted_proposal" : proposal,
	"warning_level" : warning_level,
	"warning" : warning,
    ];
}
/* run the module */
else if(func == "AskUser") {
    map stored = Lan::Export();
    symbol seq = WFM::CallFunction(`lan(.propose));
    if(seq != `next) Lan::Import(stored);
    ret = $[
	"workflow_sequence" : seq
    ];
}
/* create titles */
else if(func == "Description") {
    ret = $[
    // rich text title
	"rich_text_title" : _("Network interfaces"),
    // menu title
	"menu_title" : _("&Network interfaces"),
	"id" : "lan",
    ];
}
/* write the proposal */
else if (func == "Write") {
    if (Arch::s390 || Mode::vnc || Mode::usessh)
    {
	Lan::WriteOnly ();
    }
    else
    {
	Lan::Write ();
    }
}
/* unknown function */
else {
    y2error("unknown function: %1", func);
}

/* Finish */
y2debug("ret=%1",ret);
y2milestone("Lan proposal finished");
y2milestone("----------------------------------------");
return ret;

/* EOF */
}
