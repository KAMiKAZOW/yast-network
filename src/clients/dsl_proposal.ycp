/**
 * File:	clients/dsl_proposal.ycp
 * Package:	Network configuration
 * Summary:	DSL configuration proposal
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "network";

import "DSL";
import "Progress";

include "network/routines.ycp";

/* The main () */
y2milestone("----------------------------------------");
y2milestone("DSL proposal started");

string func = WFM::Args(0);
map param = WFM::Args(1);
map ret = $[];

/* create a textual proposal */
if(func == "MakeProposal") {
    string proposal = "";
    string warning = nil;
    symbol warning_level = nil;
    boolean force_reset = param["force_reset"]:false;

    if(force_reset || !DSL::proposal_valid) {
        DSL::proposal_valid = true;
	Progress::off();
	/* Popup text */
	BusyPopup(_("Detecting DSL devices ..."));
	DSL::Read();
	BusyPopupClose();
    }
    list sum = DSL::Summary(false);
    proposal = sum[0]:"";
    Progress::on();

    ret = $[
	"preformatted_proposal" : proposal,
	"warning_level" : warning_level,
	"warning" : warning,
    ];
}
/* run the module */
else if(func == "AskUser") {
    map stored = DSL::Export();
    symbol seq = WFM::CallFunction(`dsl(.propose));
    if(seq != `next) DSL::Import(stored);
    y2debug("stored=%1",stored);
    y2debug("seq=%1",seq);
    ret = $[
	"workflow_sequence" : seq
    ];
}
/* create titles */
else if(func == "Description") {
    ret = $[
	/* RichText label */
	"rich_text_title" : _("DSL connections"),
	/* Menu label */
	"menu_title" : _("&DSL connections"),
	"id" : "dsl",
    ];
}
/* write the proposal */
else if(func == "Write") {
    DSL::Write();
}
/* unknown function */
else {
    y2error("unknown function: %1", func);
}

/* Finish */
y2debug("ret=%1",ret);
y2milestone("DSL proposal finished");
y2milestone("----------------------------------------");
return ret;

/* EOF */
}
