/**
 * File:
 *   include/network/modem/wizards.ycp
 *
 * Package:
 *   Configuration of network
 *
 * Summary:
 *   User interface functions for modem configuration.
 *
 * Authors:
 *   Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * Wizards definitions.
 *
 */

{

textdomain "network";

import "Wizard";

include "wizard/sequencer.ycp";

include "network/modem/dialogs.ycp";
include "network/modem/complex.ycp";

//include "network/services/dns.ycp";
//include "network/services/routing.ycp";

/**
 * Whole configuration of network
 * @return sequence result
 */
global define symbol ModemSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" :
	$[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" :
	$[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    string caption = _("Modems configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of network but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol ModemAutoSequence() ``{
    string caption = _("Modems configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    symbol ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the network configuration
 * @return sequence result
 */
global define symbol MainSequence() ``{

    map aliases = $[
	"summary"	: ``(SummaryDialog()),
	"overview"	: ``(OverviewDialog()),
	"configure"	: [ ``(OneModemSequence(true)), true ],
	"other"		: [ ``(OneModemSequence(false)), true ],
	"add"		: [ ``(OneModemSequence(false)), true ],
	"edit"		: [ ``(OneModemSequence(false)), true ],
	"commit"	: [ ``(Commit()), true ],
    ];

    map sequence = $[
	"ws_start" : "summary",
	"summary" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `configure	: "configure",
	    `other	: "other",
	    `overview	: "overview"
	],
	"overview" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add",
	    `edit	: "edit",
	],
	"other" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"configure" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"commit" : $[
	    `next	: "summary",
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
    ];

    return WizardSequencer(aliases, sequence);
}

global define symbol OneModemSequence(boolean detected) ``{

    map aliases = $[
	"parameters_detected"	: ``(modem_params(`Detected)),
	"parameters"		: ``(modem_params(`other)),
	"details"		: ``(modem_details()),
	"details_detected"	: ``(modem_details()),
	"providers"		: ``(runProvidersDialog(false)),
	"provider"		: ``(runEditModemProviderDialog(true)),
	"new_provider"		: ``(runEditModemProviderDialog(false)),
	"connection"		: ``(modem_connection()),
	"connection_details"	: ``(modem_connection_details()),
    ];

    string entry = "parameters";
    if(detected) entry = "parameters_detected";

    map sequence = $[
	"ws_start" : entry,
	"parameters": $[
	    `next   : "providers",
	    `Details: "details",
	    `abort  : `abort
	],
	"parameters_detected" : $[
	    `next   : "providers",
	    `Details: "details_detected",
	    `abort  : `abort
	],
	"details"   : $[
	    `next   : "parameters",
	    `abort  : `abort
	],
	"details_detected" : $[
	    `next   : "parameters_detected",
	    `abort  : `abort
	],
	"providers" : $[
	    `next   : "provider",
	    `new    : "new_provider",
	    `abort  : `abort
	],
	"provider" : $[
	    `next   : "connection",
	    `abort  : `abort
	],
	"new_provider" : $[
	    `next   : "connection",
	    `abort  : `abort
	],
	"connection" : $[
	    `next   : `next,
	    `Details: "connection_details",
	    `abort  : `abort
	],
	"connection_details" : $[
	    `next   : "connection",
	    `abort  : `abort
	]
    ];

    return WizardSequencer(aliases, sequence);
}

/* EOF */
}
