/**
 * File:
 *   include/network/isdn/wizards.ycp
 *
 * Package:
 *   Configuration of network
 *
 * Summary:
 *   User interface functions for isdn configuration.
 *
 * Authors:
 *   Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * Wizards definitions.
 *
 */

{

textdomain "network";

import "Wizard";

include "wizard/sequencer.ycp";

include "network/isdn/ip.ycp";
include "network/isdn/complex.ycp";
include "network/isdn/callback.ycp";
include "network/isdn/lowlevel.ycp";
include "network/isdn/connection.ycp";
include "network/providers.ycp";

//include "network/services/dns.ycp";
//include "network/services/routing.ycp";

/**
 * Whole configuration of network
 * @return sequence result
 */
global define symbol ISDNSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" :
	$[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" :
	$[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    string caption = _("ISDN configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of network but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol ISDNAutoSequence() ``{
    string caption = _("ISDN configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    symbol ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the network configuration
 * @return sequence result
 */
global define symbol MainSequence() ``{

    map aliases = $[
	"summary"	: ``(SummaryDialog()),
	"overview"	: ``(OverviewDialog()),
	"configure"	: [ ``(OneISDNSequence(true)), true ],
	"other"		: [ ``(OneISDNSequence(false)), true ],
	"add"		: [ ``(OneISDNSequence(false)), true ],
	"edit"		: [ ``(OneISDNSequence(false)), true ],
	"commit"	: [ ``(Commit()), true ],
    ];

    map sequence = $[
	"ws_start" : "summary",
	"summary" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `configure	: "configure",
	    `other	: "other",
	    `overview	: "overview"
	],
	"overview" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add",
	    `edit	: "edit",
	],
	"other" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"configure" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"commit" : $[
	    `next	: "summary",
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
    ];

    return WizardSequencer(aliases, sequence);
}

global define symbol OneISDNSequence(boolean detected) ``{

    map aliases = $[
	"lowlevel_detected"	:   ``(isdn_lowlevel(`Detected)),
	"lowlevel_detectedISA"	:   ``(isdn_lowlevel(`DetectedISA)),
	"lowlevel"		:   ``(isdn_lowlevel(`other)),
	"connection"		:   ``(isdn_connection()),
	"ip"			:   ``(isdn_ip()),
	"callback"		:   ``(isdn_callback()),
	"providers"		:   ``(runProvidersDialog(true)),
	"provider"		:   ``(runEditIsdnProviderDialog()),
	"new_provider"		:   ``(runEditIsdnProviderDialog())
    ];

    string entry = "provider";
    if(detected)
	entry = "lowlevel_detected";

    /* FIXME: detected ISA unsupported */

    map sequence = $[
	"ws_start" : entry,
	"lowlevel_detected" : $[
	    `next : "providers",
	    `abort : `abort
	],
	"lowlevel_detectedISA" : $[
	    `next : "providers",
	    `abort : `abort
	],
	"lowlevel" : $[
	    `next : "providers",
	    `abort : `abort
	],
	"providers" : $[
	    `next : "provider",
	    `new  : "new_provider",
	    `abort : `abort
	],
	"provider" : $[
	    `next : "connection",
	    `abort : `abort
	],
	"new_provider": $[
	    `next : "connection",
	    `abort : `abort
	],
	"connection" : $[
	    `ipsettings : "ip",
	    `callbacksettings : "callback",
	    `next : `next,
	    `abort : `abort
	],
	"ip" : $[
	    `next : "connection",
	    `abort : `abort
	],
	"callback" : $[
	    `next : "connection",
	    `abort : `abort
	]
    ];

    return WizardSequencer(aliases, sequence);

}

/* EOF */
}
