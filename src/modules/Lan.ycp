/**
 * File:
 *   modules/Lan.ycp
 *
 * Package:
 *   Configuration of network cards
 *
 * Summary:
 *   Data for configuration of network, input and output functions.
 *
 * Authors:
 *   Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * Representation of the configuration of network.
 * Input and output routines.
 *
 */

{

module "Lan";
textdomain "network";
import "Progress";

include "network/config.ycp";
include "network/hardware.ycp";
include "network/routines.ycp";
include "network/ip.ycp";

include "ui/summary.ycp";

/*-------------*/
/* GLOBAL DATA */

/* kernel options */
global string name = "";
global string modul = "";
global string options = "";
global string unique = "";
global string type = "";

global boolean pcmcia = false;

/* address options */
global string bootproto = "static";
global string ipaddr = "";
global string netmask = "";
global integer device = -1;

/**
 * Abort function
 * return boolean return true if abort
 */
global block AbortFunction = nil;

/*--------------*/
/* PRIVATE DATA */

/**
 * Hardware information
 * [
 *     "name"	: "Digital DecCHIP 1234",
 *     "unique"	: "uihd98723hodi2hgday8",
 *     "modul"	: "tulip",
 *     "options" : ""
 * ]
 */
list HARDWARE = [];

/**
 * Config information
 */
map CONFIG = $[];

/**
 * Data was modified?
 */
boolean modified = false;

symbol operation = nil;

/*------------------*/
/* GLOBAL FUNCTIONS */

/**
 * Data was modified?
 */
global define boolean Modified() ``{
    y2debug("modified=%1",modified);
    return modified;
};

/**
 * Read all network settings from the SCR
 * @return true on success
 */
global define boolean Read() ``{

    string caption = _("Initializing network configuration");
    integer steps = 3;

    /* FIXME */
    integer sl = 0; //2000;

    Progress::New(caption, " ", steps, [
	_("Detect devices"),
	_("Read current configuration"),
	_("Detect current status"),
    ], [], "");

    sleep(sl);
    if(Abort()) return false;
    Progress::NextStage();
    Progress::Title(_("Detecting network cards"));
    ReadHardware();
    sleep(sl);

    if(Abort()) return false;
    Progress::NextStage();
    Progress::Title(_("Reading current configuration"));
    ReadConfig("eth|tr");
    sleep(sl);

    if(Abort()) return false;
    Progress::NextStage();
    Progress::Title(_("Detecting current status"));
    ReadStatus();
    sleep(sl);

    if(Abort()) return false;
    Progress::NextStage();
    Progress::Title(_("Finished"));

    sleep(sl);
    if(Abort()) return false;
    return true;
}

/**
 * Update the SCR according to network settings
 * @return true on success
 */
global define boolean Write() ``{

    string caption = _("Saving network configuration");
    integer steps = 2;

    /* FIXME */
    integer sl = 1000;

    Progress::New(caption, " ", steps, [
	_("Write configuration"),
	_("Run SuSEconfig"),
    ], [], "");

    sleep(sl);
    if(Abort()) return false;
    Progress::NextStage();
    Progress::Title(_("Writing configuration"));
    WriteConfig();
    sleep(sl);

    if(Abort()) return false;
    Progress::NextStage();
    Progress::Title(_("Running SuSEconfig"));
    RunSuSEconfig();
    sleep(sl);

    if(Abort()) return false;
    Progress::NextStage();
    Progress::Title(_("Finished"));

    sleep(sl);
    if(Abort()) return false;
    return true;
}

/**
 * Get all network settings from the first parameter
 * (for use by autoinstallation)
 * @param settings a map of settings to be imported.
 * @return true on success
 */
global define boolean Import(map settings) ``{
    return false;
}

/**
 * Dump the network settings to a single map
 * (for use by autoinstallation)
 * @return dumped settings (later acceptable by Import())
 */
global define map Export() ``{
    return $[];
}

/**
 * Build a textual summary and a list of unconfigured cards
 * @return summary of the current configuration
 */
global define list Summary() ``{

    list configured = [];
    list unconfigured = [];

    maplist(`d, HARDWARE, ``{
	string hwname = lookup(d, "name", _("Unknown network card"));
	string ty = lookup(d, "type", "eth");
	string un = lookup(d, "unique", nil);
	string status = IsConfigured(ty, un);
	y2debug("status(%1)=%2",status,d);
	if(status != "")
	    change(configured, summaryDevice(hwname, status));
	else
	    change(unconfigured, d);
    });

    list allhw = maplist(`d, HARDWARE, ``(lookup(d, "unique", nil)));
    maplist(`s, OtherConfigured(allhw), ``{
	change(configured, summaryDevice(_("Unknown network card"), s));
    });

    string summary = "";
    if(size(configured) > 0)
	summary = summaryAddHeader("", _("Already configured network cards:")) +
	    summaryDevicesList(configured);
    else
	summary = summaryAddHeader("", _("No configured network card."));

    integer selected = lookup(select(unconfigured, 0, $[]), "num", 0);
    list devs = hwlist2items(unconfigured, selected);
    if(size(devs)==0)
	change(devs, `item(`id(`other), _("Other (not detected)"), true));
    else
	change(devs, `item(`id(`other), _("Other (not detected)")));

    return [ devs, summary ];
}

/*-------------------*/
/* PRIVATE FUNCTIONS */

/**
 * Abort function
 * @return blah blah lahjk
 */
define boolean Abort() ``{
    if(AbortFunction != nil)
	return eval(AbortFunction) == true;
    return false;
}

/**
 * Read status information
 * @return true if success
 */
define boolean ReadStatus() ``{
    return true;
}

/**
 * Run SuSEconfig
 * @return true if success
 */
define boolean RunSuSEconfig() ``{
    return true;
}

/**
 * Return 10 free devices
 * @param type device type
 * @return list of 10 free devices
 */
global define list FreeDevices(string type) ``{
    y2debug("CONFIG=%1",CONFIG);
    return GetFreeDevices(type, 10);
}

/**
 * Check if the given device is configured and return
 * the corresponding device
 * @param type device type
 * @return device if configured, empty string if not
 */
global define string Configured(string type, string unique) ``{
    return IsConfigured(type, unique);
}

/**
 * Select the given hardware item or clean up structures
 * @param which item to be chosen
 */
global define void SelectHW(integer which) ``{
    map sel = $[];

    if(which != nil)
	sel = select(HARDWARE, which, $[]);

    if(which > size(HARDWARE) || which < 0)
	y2error("Item not found in HARDWARE: %1 (%2)", which, size(HARDWARE));

    name = lookup(sel, "name", "");
    unique = lookup(sel, "unique", "");
    modul = lookup(sel, "module", "");
    options = lookup(sel, "options", "");
    unique = lookup(sel, "unique", "");
    type = lookup(sel, "type", "eth");
    device = select(GetFreeDevices(type,1), 0, -1);
}

/**
 */
global define Add() ``{

    /* kernel options */
    name = "";
    modul = "";
    options = "";
    unique = "";
    type = "eth";

    pcmcia = false;

    /* address options */
    bootproto = "static";
    ipaddr = "";
    netmask = "";
    device = select(GetFreeDevices(type,1), 0, -1);

    operation = `add;
}

/**
 */
global define Edit(string typ, integer dev) ``{
// OR: global define Edit(string dev) ``{
    operation = nil;

    map typemap = lookup(CONFIG, typ, $[]);
    if(!haskey(typemap, dev)) {
	y2error("Key not found: %1", dev);
	return;
    }
    map devmap = lookup(typemap, dev, $[]);

    /* address options */
    bootproto = lookup(devmap, "BOOTPROTO", "");
    ipaddr = lookup(devmap, "IPADDR", "");
    netmask = lookup(devmap, "NETMASK", "");
    device = dev;
    type = typ;

    operation = `edit;
}

/**
 */
global define Delete() ``{
    operation = `delete;
}

/**
 */
global define Commit() ``{
    if(operation == `add || operation == `edit) {
	map newdev = $[
	    "BOOTMODE"	: "onboot",
	    "BOOTPROTO"	: bootproto,
	    "UNIQUE"	: unique,
	];
	if(bootproto == "static") {
	    change(newdev, "IPADDR", ipaddr);
	    change(newdev, "NETMASK", netmask);
	    change(newdev, "BROADCAST", compute_broadcast(ipaddr, netmask));
	    change(newdev, "NETWORK", compute_network(ipaddr, netmask));
	}
	if(operation == `add)
	    ConfigAdd(type, device, newdev);
	//else
	    //ConfigUpdate(type, device, newdev);
    }
    else if(operation == `delete) {
	ConfigDelete(type, device);
    }
    else {
	y2error("Unknown operation: %1", operation);
	return;
    }

    modified = true;
}

/* EOF */
}
