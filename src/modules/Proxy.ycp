/**
 * File:	modules/Proxy.ycp
 * Package:	Network configuration
 * Summary:	Proxy data
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

module "Proxy";
textdomain "network";

include "network/routines.ycp";
include "network/runtime.ycp";

/**
 * Data was modified?
 */
global boolean modified = false;

global boolean enabled = true;
global string http = "";
global string ftp = "";
global string no = "";
global string user = "";
global string pass = "";

/**
 * Read settings
 * @return true if success
 */
global define boolean Read() ``{

    /* Read /etc/sysconfig/proxy */
    http = SCR::Read(.sysconfig.proxy.HTTP_PROXY);
    if(http == nil) http = "";
    ftp = SCR::Read(.sysconfig.proxy.FTP_PROXY);
    if(ftp == nil) ftp = "";
    no = SCR::Read(.sysconfig.proxy.NO_PROXY);
    if(no == nil) no = "";
    enabled = SCR::Read(.sysconfig.proxy.PROXY_ENABLED) != "no";

    /* Read /root/.curlrc */
    user = SCR::Read(.root.curlrc.proxy-user);
    if(user == nil) user = "";

    if(issubstring(user, ":")) {
	pass = regexpsub(user, "^.*:(.*)$", "\\1");
	user = regexpsub(user, "^(.*):.*$", "\\1");
    }
    if(pass == nil) pass = "";
    if(user == nil) user = "";

    /* Read /root/.wgetrc */
    /* YOU uses curl(1)
    user = SCR::Read(.root.wgetrc.proxy_user);
    if(user == nil) user = "";
    pass = SCR::Read(.root.wgetrc.proxy_passwd);
    if(pass == nil) pass = "";
    */

    return true;
}

/**
 * Write routing settings and apply changes
 * @return true if success
 */
global define boolean Write() ``{

    if(!modified) return true;
    y2milestone("Writing configuration");

    /* Update /etc/sysconfig/proxy */
    SCR::Write(.sysconfig.proxy.PROXY_ENABLED, enabled?"yes":"no");
    SCR::Write(.sysconfig.proxy.HTTP_PROXY, http);
    SCR::Write(.sysconfig.proxy.FTP_PROXY, ftp);
    SCR::Write(.sysconfig.proxy.NO_PROXY, no);
    SCR::Write(.sysconfig.proxy, nil);

    /* Update /root/.curlrc */
    string proxyuser = nil;
    if(user != "") {
	proxyuser = user;
	if(pass != "") proxyuser = user + ":" + pass;
    }
    SCR::Write(.root.curlrc.proxy-user, proxyuser);
    SCR::Write(.root.curlrc.proxy-user.comment, ChangedComment("proxy"));
    SCR::Write(.root.curlrc, nil);

    /* Update /root/.wgetrc */
    /* YOU uses curl(1)
    SCR::Write(.root.wgetrc.proxy_user, user);
    SCR::Write(.root.wgetrc.proxy_passwd, pass);
    SCR::Write(.root.wgetrc, nil); */

    /* Update profiles generated by SuSEconfig */
    RunSuSEconfigModule("profiles");
    modified = false;

    return true;
}


/**
 * Get all settings from a map.
 * When called by <name>_auto (preparing autoinstallation data)
 * the map may be empty.
 * @param settings autoinstallation settings
 * @return true if success
 */
global define boolean Import(map settings) ``{
    if(size(settings) == 0)
	return true;

    enabled = settings["enabled"]:true;
    http = settings["http_proxy"]:"";
    ftp = settings["ftp_proxy"]:"";
    no = settings["no_proxy"]:"localhost";
    user = settings["proxy_user"]:"";
    pass = settings["proxy_password"]:"";

    modified = true;
    return true;
}

/**
 * Dump the Routing settings to a map, for autoinstallation use.
 * @return autoinstallation settings
 */
global define map Export() ``{
    map settings = $[
	"enabled"	: enabled,
	"http_proxy"	: http,
	"ftp_proxy"	: ftp,
	"no_proxy"	: no,
	"proxy_user"	: user,
	"proxy_password": pass,
    ];
    return settings;
}

/* EOF */
}
