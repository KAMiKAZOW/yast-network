/**
 * File:	modules/Internet.ycp
 * Package:	Network configuration
 * Summary:	Internet data
 * Authors:	Michal Svec <msvec@suse.cz>
 *		Arvin Schnell <arvin@suse.de>
 *
 * $Id$
 */

{

module "Internet";
textdomain "network";

include "network/complex.ycp";
include "network/devices.ycp";
include "network/routines.ycp";

// flag to remember if user wants to run internet test
global boolean do_test = true;

// flag to remember if you should be started
global boolean do_you = false;

global string device = "";
global string password = "";
global boolean demand = false;
global boolean askpassword = nil;

/**
 * Devices in order from fastest to slowest
 */
map FastestRegexps = $[
    1 : NetworkDeviceRegex,
    2 : "dsl",
    3 : ISDNDeviceRegex,
    4 : "modem|ppp",
];

/**
 * Types in order from fastest to slowest
 * @see FastestRegexps
 */
map FastestTypes = $[
    1 : "netcard",
    2 : "dsl",
    3 : "isdn",
    4 : "modem",
];

/**
 * Return the fastest connection type
 * @param dev device name
 * @return device type
 * @see FastestTypes
 * @example FastestType("ippp") -> "isdn"
 * @example FastestType("ppp") -> "modem"
 */
global define string FastestType(string dev) ``{
    string type = "";
    maplist(integer num, string regex, FastestRegexps, ``{
	if(type == "" && regexpmatch(dev, "^" + regex + "[0-9]*$"))
	    type = FastestTypes[num]:"";
    });
    return type;
}


/**
 * Return the filename of the log for the fastest connection.
 */
global define string FastestLogFile (string dev) ``{
    string file = "/var/log/messages";
    string type = FastestType (dev);
    if (type == "dsl" || type == "modem")
	file = "/var/log/smpppd/ifcfg-" + dev + ".log";
    return file;
}


/**
 * Find the fastest connection to the Internet
 * @return device with the fastest connection
 */
global define boolean Fastest() ``{

    /* Read available devcies */
    map Devices = $[];
    ReadDevices("");
    y2debug("Devices=%1", Devices);
    list types = mapkeys(Devices);
    y2debug("types=%1", types);
    list devices = [];
    maplist(string t, types, ``{
	devices = union(devices, maplist(string n, mapkeys(Devices[t]:$[]), ``(t+n)));
    });
    y2debug("devices=%1", devices);

    /* Find the fastest device */
    device = "";
    maplist(integer num, string regex, FastestRegexps, ``{
	maplist(string dev, devices, ``{
	    if(device == "" && regexpmatch(dev, "^" + regex + "[0-9]*$"))
		device = dev;
	});
    });
    y2milestone("fastest=%1", device);

    /* No fallback since there are devices that should not be tested (lo) */
    if(device == "") return false;

    /* Handle DSL PPPoE ethernet cards */
    if(device == "eth0" && contains(devices, "dsl0")) {
	map ethmap = Devices[device_type("eth0"), device_num("eth0")]:$[];
	map dslmap = Devices[device_type("dsl0"), device_num("dsl0")]:$[];
	/* Only DSL cards with correct device */
	if(dslmap["DEVICE"]:"" == "eth0") {
	    device = "dsl0";
	}
    }

    map devmap = Devices[device_type(device), device_num(device)]:$[];
    y2debug("devmap=%1", devmap);

    /* Lookup the provider values */
    string provider = devmap["PROVIDER"]:"";
    if (provider != "") {
	import "Provider";
	Provider::Read("all");
	Provider::Select(provider);
	demand = Provider::Provider["DEMAND"]:"no" == "yes";
	password = Provider::Provider["PASSWORD"]:"";
	askpassword = Provider::Provider["ASKPASSWORD"]:"no" == "yes";
    }

    return true;
}

/**
 * Start the fastest interface
 * @return true if successful started
 */
global define boolean Start() ``{

    if (askpassword == "yes")
	return SCR::Execute (.target.bash_input, "/sbin/ifup " + device, password) == 0;
    else
	return SCR::Execute (.target.bash, "/sbin/ifup " + device) == 0;
}

/**
 * Stop the fastest interface
 * @return true if successful stopped
 */
global define boolean Stop() ``{
    return SCR::Execute(.target.bash, "/sbin/ifdown " + device) == 0;
}

/**
 * Status of the fastest interface
 * @return true if interface is up (which is not equal to connected)
 */
global define boolean Status() ``{
    return SCR::Execute(.target.bash, "/sbin/ifstatus " + device) == 0;
}


global define boolean Connected() ``{

    string type = FastestType (device);

    if (type == "dsl" || type == "modem") {
	string tmp1 = SCR::Read (.target.string, "/var/lib/smpppd/ifcfg-" +
				 device + ".info");
	list tmp2 = splitstring (tmp1, "\n");
	return contains (tmp2, "status: connected");
    }

    if (type == "isdn") {
	// FIXME
    }

    return true;
}


/* EOF */
}
