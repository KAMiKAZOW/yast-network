/**
 * File:	modules/NetworkDevices.ycp
 * Package:	Network configuration
 * Summary:	Device manipulation functions
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

module "NetworkDevices";
textdomain "network";

import "Arch";
import "Map";

/* NI: required wrt functions in routines.ycp */
block<boolean> AbortFunction = nil;
define boolean Modified() ``{ return true; }

include "network/routines.ycp";

/**
 * Current device information
 */
global map Current = $[];

global string name = "";
global string unique = "";

global string type = "";
global string device = "";
global string alias = "";

/**
 * Hotplug type ("" if not hot pluggable)
 */
global string hotplug = "";

/**
 * Devices information
 * @see ReadDevices
 */
map<string,map> Devices = $[];

/**
 * Devices information
 * @see ReadDevices
 */
map<string,map> OriginalDevices = $[];

/**
 * Deleted devices
 */
list<string> Deleted = [];

/**
 * True if devices are already read
 */
boolean initialized = false;

/**
 * Read devices from files
 * @return true if sucess
 */
global define boolean Read() ``{

    Devices = $[];

    if(initialized == true) return true;

    /* Variables which could be suffixed and thus duplicated */
    list Locals = [ "IPADDR", "REMOTE_IPADDR", "NETMASK", "PREFIXLEN",
	    "BROADCAST", "SCOPE", "LABEL", "IP_OPTIONS" ];

    /* preparation */
    list allfiles = SCR::Dir(.network.section);
    list devices = filter(string file, allfiles, ``(!regexpmatch(file, "[\.~]")));
    y2debug("devices=%1", devices);

    devices = filter(string d, devices, ``(regexpmatch(d, "[a-z][a-z-]*[0-9]*")));
    y2debug("devices=%1", devices);

    /* Read devices */
    maplist(string d, devices, ``{
	string devtype = device_type(d);

	string devnum = "";
	if(regexpmatch(d, "[a-z][a-z-]*[0-9]+"))
	    devnum = sformat("%1", device_num(d));
	y2debug("devnum=%1", devnum);

	map dev = Devices[devtype]:$[];
	if(haskey(dev, devnum)) {
	    y2error("device already present: %1", devnum);
	    return;
	}

	string pth = ".network.value.\"" + device_name(devtype, devnum) + "\"";
	list values = SCR::Dir(topath(pth));

	map config = $[];
	maplist(string val, values, ``{
	    string item = (string) SCR::Read(topath(pth + "." + val));
	    if(item == nil) return;
	    /* No underscore '_' -> global */
	    /* Also temporarily standard globals */
	    if(find(val, "_") < 0 || contains(Locals, val)) {
		config[val] = item;
		return;
	    }
	    /* Try to strip _suffix */
	    string v = substring(val, 0, findlastof(val, "_"));
	    string s = substring(val, findlastof(val, "_"));
	    if(size(s) > 1) s = substring(s, 1);
	    y2milestone("%1:%2:%3", val, v, s);
	    /* Global */
	    if(!contains(Locals, v))
		config[val] = item;
	    /* Local */
	    else {
		map _aliases = config["_aliases"]:$[];
		map suf = _aliases[s]:$[];
		suf[v] = item;
		_aliases[s] = suf;
		config["_aliases"] = _aliases;
	    }
	    y2milestone("config=%1", config);
	});

	change(dev, devnum, config);
	change(Devices, devtype, dev);
    });
    y2debug("Devices=%1", Devices);

    initialized = true;
    return true;
}

/**
 */
define map Filter(map devices, string devregex) ``{
    if(devregex == nil || devregex == "")
	return devices;

    string regex = "^(" + devregex + ")[0-9]*$";
    y2debug("regex=%1", regex);
    devices = filter(string file, string devmap, devices, ``{
	return regexpmatch(file, regex) == true;
    });
    y2debug("devices=%1", devices);
    return devices;
}

/* Code backup:
    if(devregex != nil && devregex != "") {
	string regex = "^(" + devregex + ")[0-9]*$";
	y2debug("regex=%1", regex);
	devices = filter(string file, devices, ``(regexpmatch(file, regex)));
	y2debug("devices=%1", devices);
    }
*/

/**
 * Write devices to files
 * @param devregex regular expression for the device type
 * @return true if success
 * @example NetworkDevice::Write("eth|tr");
 */
global define boolean Write(string devregex) ``{

    y2debug("Devices=%1", Devices);

    map Devs = Filter(Devices, devregex);
    map OriginalDevs = Filter(OriginalDevices, devregex);
    y2debug("OriginalDevs=%1", OriginalDevs);
    y2debug("Devs=%1", Devs);

    /* Check for changes */
    if(Devs == OriginalDevs) {
	y2debug("No changes to %1 devices -> nothing to write", type);
	return true;
    }

    /* remove deleted devices */
    foreach(string d, Deleted, ``{
	if(!haskey(OriginalDevs, d)) return;
	/* delete config file */
	path p = topath(".network.section.\"" + d + "\"");
	y2debug("deleting: %1", p);
	SCR::Write(p, nil);
    });
    Deleted = filter(string dev, Deleted, ``(haskey(OriginalDevs, dev)));

    /* Devices with chmod=0600 */
    list chmod = [];

    /* write all devices */
    maplist(string typ, map devsmap, Devs, ``{
	maplist(string num, map devmap, devsmap, ``{
	    /* write sysconfig */
	    string dev = device_name(typ, num);
	    string p = ".network.value.\"" + dev + "\".";

	    /* write all keys to config */
	    maplist(string k, Map::Keys(devmap), ``{
		/* Write aliases */
		if(k == "_aliases") {
		    maplist(string anum, map amap, devmap[k]:$[], ``{
			maplist(string ak, string av, amap, ``{
			    string akk = ak + "_" + anum;
			    SCR::Write(topath(p + akk), av);
			});
		    });
		}
		/* Write regular keys */
		else
		    SCR::Write(topath(p + k), devmap[k]:"");
	    });

	    /* update libhd unique number */
	    // FIXME: move it somewhere else: hardware
	    string unq = devmap["UNIQUE"]:"";
	    if(unq != "") SCR::Write(.probe.status.configured, unq, `yes);

	    /* 0600 if contains encryption key (#24842) */
	    string key = devmap["WIRELESS_KEY"]:"";
	    string file = "/etc/sysconfig/network/ifcfg-" + dev;
	    y2debug("Permission change: %1, %2", key != "", file);
	    if(key != "") {
		y2debug("CHANGED");
		change(chmod, file);
	    }
	});
    });

    /* Finish him */
    SCR::Write(.network, nil);

    /* CHMOD */
    y2debug("chmod=%1", chmod);
    maplist(string file, chmod, ``{
	y2debug("changing: %1", file);
	SCR::Execute(.target.bash, "/bin/chmod 0600 " + file);
    });

    return true;
}

/**
 * Compute free devices
 * @param type device type
 * @param num how many free devices return
 * @return num of free devices
 * @example GetFreeDevices("eth", 2) -&gt; [ 1, 2 ]
 */
global define list GetFreeDevices(string type, integer num) ``{
    y2debug("Devices=%1", Devices);
    y2debug("type,num=%1,%2", type, num);
    y2debug("Devices[%1]=%2", type, Devices[type]:$[]);

    list curdevs = Map::Keys(Devices[type]:$[]);
    y2debug("curdevs=%1", curdevs);

    integer i = 0;
    integer count = 0;
    list ret = [];

    /* Hotpluggable devices */
    if(IsHotplug(type) && !contains(curdevs, "")) {
	y2debug("Added simple hotplug device");
	count = count + 1;
	change(ret, "");
    }

    /* Remaining numbered devices */
    while(count < num) {
	string ii = sformat("%1", i);
	if(!contains(curdevs, ii)) {
	    change(ret, ii);
	    count = count + 1;
	}
	i = i + 1;
    }

    y2debug("Free devices=%1", ret);
    return ret;
}

/**
 * Return free device
 * @param type device type
 * @return free device
 * @example GetFreeDevice("eth") -&gt; "1"
 */
global define string GetFreeDevice(string type) ``{
    y2debug("type=%1", type);
    string ret = (string) select(GetFreeDevices(type, 1), 0, nil);
    if(ret == nil) y2error("Free device location error: %1", ret);
    y2debug("Free device=%1", ret);
    return ret;
}

/**
 * Update Devices map
 * @param type device type
 * @param device device number
 * @param newdev new device map
 * @param check if check if device already exists
 * @return true if success
 */
global define boolean ChangeDevice(string type, string device, map newdev, boolean check) ``{
    y2debug("ChangeDevice(%1,%2,%3,%4)", type, device, newdev, check);
    y2debug("Devices=%1", Devices);
    map devmap = Devices[type]:$[];
    string dev = sformat("%1", device);

    if(check && haskey(devmap, dev)) {
	y2error("Key already present: %1(%2)", dev, type);
	return false;
    }
    change(devmap, dev, newdev);
    change(Devices, type, devmap);
    y2debug("Devices=%1", Devices);
    return true;
}

/**
 * Check presence of device (alias) and fill in type|device|alias variables.
 * @param dev device identifier
 * @return true if device is present
 */
global define boolean CheckDevice(string dev) ``{

    y2debug("CheckDevice(%1)", dev);
    string typ = device_type(dev);
    string num = device_num(dev);
    string anum = alias_num(dev);
    y2debug("CheckDevice(%1,%2,%3)", typ, num, anum);

    if(!haskey(Devices, typ))
	return false;

    map devsmap = Devices[typ]:$[];
    if(!haskey(devsmap, num))
	return false;

    type = typ;
    device = num;
    alias = "";

    if(anum != "") {
	map devmap = devsmap[num]:$[];
	map amap = devmap["_aliases"]:$[];
	if(!haskey(amap, anum))
	    return false;
	alias = anum;
    }

    y2debug("CheckDevice(%1,%2,%3)", type, device, alias);
    return true;
}

/**
 * Update Devices map
 * @param dev device identifier
 * @param newdev new device map
 * @param check if check if device already exists
 * @return true if success
 */
global define boolean ChangeDevice2(string dev, map newdev, boolean check) ``{
    y2debug("ChangeDevice(%1,%2,%3)", dev, newdev, check);
    y2debug("ChangeDevice(%1,%2,%3)", type, device, hotplug);
    y2debug("Devices=%1", Devices);

    if(CheckDevice(dev) && check) {
	y2error("Device already present: %1", dev);
	return false;
    }

    string t = device_type(dev);
    string d = device_num(dev);
    string a = alias_num(dev);
    y2debug("ChangeDevice(%1,%2,%3)", t, d, a);

    map devsmap = Devices[t]:$[];
    map devmap = devsmap[d]:$[];
    map amap = devmap["_aliases"]:$[];

    if(a != "")
	change(amap, a, newdev);
    else
	devmap = newdev;

    change(devmap, "_aliases", amap);
    change(devsmap, d, devmap);
    change(Devices, t, devsmap);

    y2debug("Devices=%1", Devices);
    return true;
}

/**
 * Delete a device from Devices map
 * @param type device type
 * @param dev device number
 * @return true if success
 */
global define boolean DeleteDevice(string type, string dev) ``{
    y2debug("Devices=%1", Devices);
    y2debug("Deleted=%1", Deleted);

    map devmap = Devices[type]:$[];
    if(!haskey(devmap, dev)) {
	y2error("Key not found: %1(%2)", dev, type);
	return false;
    }
    //remove(devmap, dev);
    devmap = remove(devmap, dev);
    change(Devices, type, devmap);
    change(Deleted, type+dev);

    y2debug("Devices=%1", Devices);
    y2debug("Deleted=%1", Deleted);
    return true;
}

/**
 * Delete a device from Devices map
 * @param dev device identifier
 * @return true if success
 */
global define boolean DeleteDevice2(string dev) ``{
    y2debug("Devices=%1", Devices);
    y2debug("Deleted=%1", Deleted);
    y2debug("DeleteDevice(%1)", dev);

    if(!CheckDevice(dev)) {
	y2error("Device not found: %1", dev);
	return false;
    }

    map devsmap = Devices[type]:$[];

    if(alias != "") {
	map devmap = devsmap[device]:$[];
	map amap = devmap["_aliases"]:$[];
	amap = remove(amap, alias);
	change(devmap, "_aliases", amap);
	change(devsmap, device, devmap);
    }
    else
	devsmap = remove(devsmap, device);

    change(Devices, type, devsmap);
    change(Deleted, dev);

    y2debug("Devices=%1", Devices);
    y2debug("Deleted=%1", Deleted);
    return true;
}

/**
 * Locate devices of the given type and value
 * @param key device key
 * @param val device value
 * @return list of devices with key=val
 */
global define list Locate(string key, string val) ``{
    list ret = [];
    maplist(string typ, map devsmap, Devices, ``{
	maplist(string num, map devmap, devsmap, ``{
	    if(devmap[key]:"" == val) change(ret, typ+num);
	});
    });

    return ret;
}

/**
 * Update /dev/modem symlink
 * @return true if success
 */
global define boolean UpdateModemSymlink() ``{
    boolean ret = false;
    if(contains(Map::Keys(Devices), "ppp")) {
	list ml = Map::Keys(Devices["ppp"]:$[]);
	string ms = ml[0]:"0";
	// map mm = lookup(Devices["ppp"]:$[], ms, $[]);
	map mm = Devices["ppp", ms]:$[];
	string mdev = mm["MODEM_DEVICE"]:"";
	if(mdev != "" && mdev != "/dev/modem") {
	    string curlink = nil;
	    map m = (map) SCR::Read(.target.lstat, "/dev/modem");
	    if(m["islink"]:false == true)
		curlink = (string) SCR::Read(.target.symlink, "/dev/modem");
	    if(curlink != mdev) {
		SCR::Execute(.target.symlink, mdev, "/dev/modem");
		ret = true;
	    }
	}
    }
    return ret;
}

/**
 * Clean the hotplug devices compatibility symlink,
 * usually ifcfg-eth-pcmcia -> ifcfg-eth-pcmcia-0.
 * @return true if success
 */
global define boolean CleanHotplugSymlink() ``{

    list types = [ "eth-pcmcia", "eth-usb", "tr-pcmcia", "tr-usb" ];
    maplist(string t, types, ``{
	string link = "/etc/sysconfig/network/ifcfg-" + t;
	y2debug("link=%1", link);
	if(lookup((map)SCR::Read(.target.lstat, link), "islink", false) == true) {
	    string file = (string) SCR::Read(.target.symlink, link);
	    file = "/etc/sysconfig/network/" + file;
	    y2debug("file=%1", file);
	    if(SCR::Read(.target.size, file) > -1) {
		y2milestone("Cleaning hotplug symlink");
		y2milestone("Devices[%1]=%2", t, Devices[t]:$[]);
		Devices[t] = remove(Devices[t]:$[], "");
		y2milestone("Devices[%1]=%2", t, Devices[t]:$[]);
	    }
	}
    });
}

/* EOF */
}
