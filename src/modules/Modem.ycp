/**
 * File:
 *   modules/Modem.ycp
 *
 * Package:
 *   Configuration of network
 *
 * Summary:
 *   Data for configuration of modem, input and output functions.
 *
 * Authors:
 *   Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * Representation of the configuration of modem.
 * Input and output routines.
 *
 */

{

module "Modem";
textdomain "network";

import "Progress";
import "Summary";

include "network/config.ycp";
include "network/hardware.ycp";
include "network/routines.ycp";
include "network/summary.ycp";
include "network/ip.ycp";

/* general stuff */
global string name = "";
global string type = "";
global integer device = -1;
global string unique = "";

/* modem settings */
global string Init1 = "ATZ";
global string Init2 = "AT Q0 V1 E1 S0=0 &C1 &D2 +FCLASS=0";
global string Init3 = "";
global integer BaudRate = 57600;

global boolean PulseDial = true;
global boolean Carrier = true;
global boolean Speaker = true;

global string Device = "/dev/modem";
global string DialPrefix = "";
global string PPPDoptions = "";

/* provider settings */
global boolean DialOnDemand = false;
global string DNS1 = "";
global string DNS2 = "";

global boolean AutoDNS = true;
global string IdleSeconds = "300";

global string provName = "";
global string provShortName = "";
global boolean provAskPassword = false;

global string provUsername = "";
global string provPassword = "";
global string provPhone = "";

/* obsolete stuff */
// global string DefaultModem = "";
// global boolean IsDefault = true;
// global string Provider = "";

/* other stuff */
global boolean Firewall = false;

/*--------------*/
/* PRIVATE DATA */

/**
 * Config information
 */
map CONFIG = $[];

/**
 * Hardware information
 * [
 *     "name"	: "Digital DecCHIP 1234",
 *     "unique"	: "uihd98723hodi2hgday8",
 * ]
 */
list HARDWARE = [];

/**
 * Deleted devices
 */
list DELETED = [];

/**
 * Abort function
 * return boolean return true if abort
 */
global block AbortFunction = nil;

/**
 * Data was modified?
 */
boolean modified = false;

/**
 * Which operation is pending?
 */
symbol operation = nil;

/* FIXME */
map WriteOneProvider = $[];

/*------------------*/
/* GLOBAL FUNCTIONS */

/**
 * Data was modified?
 * @return true if modified
 */
global define boolean Modified() ``{
    y2debug("modified=%1",modified);
    return modified;
};

/**
 * Read all network settings from the SCR
 * @return true on success
 */
global define boolean Read() ``{

    string caption = _("Initializing modem configuration");
    integer steps = 2;

    /* FIXME */
    integer sl = 0; //1000;
    sleep(sl);

    Progress::New(caption, " ", steps, [
        _("Detect devices"),
        _("Read current configuration"),
        //_("Read routing configuration"),
        //_("Read hostname and DNS configuration"),
        //_("Read installation information"),
        //_("Detect current status"),
    ], [], "");

    if(Abort()) return false;
    ProgressNextStage(_("Detecting modems"));
    ReadHardware("ppp");
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Reading current configuration"));
    ReadConfig("ppp");
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Finished"));
    sleep(sl);

    if(Abort()) return false;
    return true;
}

/**
 * Update the SCR according to network settings
 * @return true on success
 */
global define boolean Write() ``{

    string caption = _("Saving network configuration");
    integer steps = 2;

    /* FIXME */
    integer sl = 0; //1000;
    sleep(sl);

    Progress::New(caption, " ", steps, [
        _("Write configuration"),
        _("Write provider information"),
    ], [], "");

    if(Abort()) return false;
    ProgressNextStage(_("Writing configuration"));
    WriteConfig();
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Writing providers"));
    //WriteProviders();
    sleep(sl);

    if(Abort()) return false;
    return true;
}

/**
 * Select the given hardware item or clean up structures
 * @param which item to be chosen
 */
// FIXME: -> routines/hardware.ycp (?)
global define void SelectHW(integer which) ``{
    map sel = $[];

    if(which != nil)
	sel = select(HARDWARE, which, $[]);

    if(which > size(HARDWARE) || which < 0)
	y2error("Item not found in HARDWARE: %1 (%2)", which, size(HARDWARE));

    name = lookup(sel, "name", "");
    unique = lookup(sel, "unique", "");
    type = lookup(sel, "type", "eth");
    device = select(GetFreeDevices(type,1), 0, -1);

    BaudRate = sel["speed"]:"115200";
    Device = sel["device_name"]:"";
    Init1 = sel["init1"]:"";
    Init2 = sel["init2"]:"";
    PPPDoptions = sel["pppd_options"]:"";
}

/**
 * Add a new modem or edit existing (prepare before Commit)
 * @return true if success
 */
global define boolean AddEdit(string dev) ``{
    operation = nil;
    map devmap = $[];

    /* dev=="" -> Add */
    if(dev == "") {
	type = "ppp";
	device = select(GetFreeDevices(type,1), 0, -1);
    }
    /* dev!="" -> Edit */
    else {
	string typ = device_type(dev);
	integer num = device_num(dev);
	string n = sformat("%1",num);

	map typemap = lookup(CONFIG, typ, $[]);
	if(!haskey(typemap, n)) {
	    y2error("Key not found: %1", dev);
	    return false;
	}
	devmap = lookup(typemap, n, $[]);

	type = typ;
	device = num;
    }

    /* general stuff */
    name = devmap["NAME"]:"";
    unique = devmap["UNIQUE"]:"";

    /* modem settings */
    Init1 = devmap["INIT1"]:"ATZ";
    Init2 = devmap["INIT2"]:"AT Q0 V1 E1 S0=0 &C1 &D2 +FCLASS=0";
    Init3 = devmap["INIT3"]:"";
    BaudRate = devmap["SPEED"]:57600;

    Speaker = devmap["INIT8"]:"ATM1" == "ATM1";
    Carrier = devmap["INIT9"]:"" == "";
    PulseDial = devmap["DIALCOMMAND"]:"ATDP" == "ATDP";

    Device = devmap["MODEM_DEVICE"]:"/dev/modem";
    DialPrefix = devmap["DIALPREFIX"]:"";
    PPPDoptions = devmap["PPPD_OPTIONS"]:"";

    /* provider settings */
    DialOnDemand = devmap["DEMAND"]:"no" == "yes";
    DNS1 = devmap["DNS1"]:"";
    DNS2 = devmap["DNS2"]:"";

    AutoDNS = devmap["AUTODNS"]:"yes" == "yes";
    IdleSeconds = devmap["IDLETIME"]:"0";

    provPhone = devmap["PHONE"]:"";
    provUsername = devmap["USERNAME"]:"";
    provPassword = devmap["PASSWORD"]:"";

    provName = devmap["PRODUCT"]:"";
    provShortName = devmap["PROVIDER"]:"";
    provAskPassword = devmap[""]:"no" == "yes";

    //IsDefault = true;
    //DefaultModem = "";
    //Provider = devmap["PROVIDER"]:"";

    /* other stuff */
    Firewall = devmap["FIREWALL"]:"no" == "yes";

    operation = `edit;
    return true;
}

/**
 * Delete a modem (prepare before Commit)
 * @return true if success
 */
global define boolean Delete(string dev) ``{
    operation = nil;
    string typ = device_type(dev);
    integer num = device_num(dev);

    map typemap = lookup(CONFIG, typ, $[]);
    if(!haskey(typemap, sformat("%1",num))) {
        y2error("Key not found: %1", dev);
        return false;
    }

    type = typ;
    device = num;
    operation = `delete;
    return true;
}

/**
 * Commit the pending operation
 * @return true if success
 */
global define boolean Commit() ``{
    y2debug("Commit(%1)",operation);
    if(operation == `add || operation == `edit) {
	map newdev = $[
	    "BOOTMODE"	: "off",
	    "BOOTPROTO"	: "none",
	    "UNIQUE"	: unique,
	    "INIT1"	: Init1,
	    "INIT2"	: Init2,
	    "SPEED"	: BaudRate,
	    "INIT8"	: Carrier ? "" : "ATX3",
	    "INIT9"	: Speaker ? "ATM1" : "ATM0",
	    "DIALCOMMAND"	: PulseDial ? "ATDP" : "ATDT",
	    "MODEM_DEVICE"	: Device,
	    "DIALPREFIX"	: DialPrefix,
	    "PROVIDER"		: provShortName,
	    "PPPD_OPTIONS"	: PPPDoptions,
	];

	if(operation == `add)
	    ConfigAdd(type, device, newdev, true);
	else
	    ConfigAdd(type, device, newdev, false);

	/* FIXME: provider settings */
	WriteOneProvider["DEMAND"] = DialOnDemand;
	WriteOneProvider["DNS1"] = DNS1;
	WriteOneProvider["DNS2"] = DNS2;

	WriteOneProvider["AUTODNS"] = AutoDNS;
	WriteOneProvider["IDLETIME"] = IdleSeconds;

	WriteOneProvider["PHONE"] = provPhone;
	WriteOneProvider["USERNAME"] = provUsername;
	WriteOneProvider["PASSWORD"] = provPassword;

	WriteOneProvider["PRODUCT"] = provName;
	WriteOneProvider["PROVIDER"] = provShortName;
	WriteOneProvider["ASKPASSWORD"] = provAskPassword;
	/* FIXME */

    }
    else if(operation == `delete) {
	ConfigDelete(type, device);
	change(DELETED, type+device);
    }
    else {
	y2error("Unknown operation: %1", operation);
	return false;
    }

    modified = true;
    operation = nil;
    return true;
}

global define boolean Import(map settings) ``{
    CONFIG = settings;
}

global define map Export() ``{
    return CONFIG;
}

/**
 * Build a textual summary and a list of unconfigured cards
 * @return summary of the current configuration
 */
global define list Summary(boolean split) ``{
    return BuildSummary(split);
}

/**
 */
global define list Overview() ``{
    return BuildOverview("ppp");
}

/**
 */
define boolean Abort() ``{
    if(AbortFunction != nil)
        return eval(AbortFunction) == true;
    return false;
}

/* EOF */
}
