/**
 * File:
 *   modules/Modem.ycp
 *
 * Package:
 *   Configuration of network
 *
 * Summary:
 *   Data for configuration of modem, input and output functions.
 *
 * Authors:
 *   Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * Representation of the configuration of modem.
 * Input and output routines.
 *
 */

{

module "Modem";
textdomain "network";

import "Firewall";
import "Providers";
import "Progress";
import "Summary";

include "network/config.ycp";
include "network/hardware.ycp";
include "network/routines.ycp";
include "network/runtime.ycp";
include "network/summary.ycp";
include "network/ip.ycp";

/* general stuff */
global string name = "";
global string type = "";
global integer device = -1;
global string unique = "";

/* modem settings */
global string Init1 = "ATZ";
global string Init2 = "AT Q0 V1 E1 S0=0 &C1 &D2 +FCLASS=0";
global string Init3 = "";
global integer BaudRate = 57600;

global boolean PulseDial = true;
global boolean Carrier = true;
global boolean Speaker = true;

global string Device = "/dev/modem";
global string DialPrefix = "";
global string PPPDoptions = "";

/* obsolete stuff */
// global string DefaultModem = "";
// global boolean IsDefault = true;
// global string Provider = "";

/* something already proposed? */
global boolean proposal_valid = false;

/*--------------*/
/* PRIVATE DATA */

/**
 * Config information
 */
map CONFIG = $[];

/**
 * Hardware information
 * [
 *     "name"	: "Digital DecCHIP 1234",
 *     "unique"	: "uihd98723hodi2hgday8",
 * ]
 */
list HARDWARE = [];

/**
 * Deleted devices
 */
list DELETED = [];

/**
 * Abort function
 * return boolean return true if abort
 */
global block AbortFunction = nil;

/**
 * Data was modified?
 */
boolean modified = false;

/**
 * Which operation is pending?
 */
symbol operation = nil;

/*------------------*/
/* GLOBAL FUNCTIONS */

/**
 * Data was modified?
 * @return true if modified
 */
global define boolean Modified() ``{
    y2debug("modified=%1",modified);
    return modified;
};

/**
 * Read all network settings from the SCR
 * @return true on success
 */
global define boolean Read() ``{

    string caption = _("Initializing modem configuration");
    integer steps = 4;

    /* FIXME */
    integer sl = 0; //1000;
    sleep(sl);

    Progress::New(caption, " ", steps, [
        _("Detect devices"),
        _("Read current configuration"),
	_("Read firewall settings"),
        _("Read providers"),
    ], [], "");

    if(Abort()) return false;
    ProgressNextStage(_("Detecting modems"));
    ReadHardware("modem");
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Reading current configuration"));
    ReadConfig("ppp");
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Reading firewall settings"));
    Firewall::Read();
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Reading providers"));
    Providers::Read();
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Finished"));
    sleep(sl);

    if(Abort()) return false;
    modified = false;
    return true;
}

/**
 * Update the SCR according to network settings
 * @return true on success
 */
global define boolean Write() ``{

    if(!modified) return true;
    y2milestone("Writing configuration");

    string caption = _("Saving modem configuration");
    integer steps = 4;

    /* FIXME */
    integer sl = 0; //1000;
    sleep(sl);

    Progress::New(caption, " ", steps, [
        _("Write configuration"),
        _("Write firewall settings"),
        _("Write providers"),
        _("Set up smpppd"),
    ], [], "");

    if(Abort()) return false;
    ProgressNextStage(_("Writing configuration"));
    WriteConfig();
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Writing firewall settings"));
    Firewall::Write();
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Writing providers"));
    Providers::Write();
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Setting up smpppd(8)"));
    SetupSMPPPD();
    sleep(sl);

    if(Abort()) return false;
    return true;
}

/**
 * Add a new modem or edit existing (prepare before Commit)
 * @return true if success
 */
global define boolean AddEdit(string dev) ``{
    operation = nil;
    map devmap = $[];

    /* dev=="" -> Add */
    if(dev == "") {
	type = "ppp";
	device = select(GetFreeDevices(type,1), 0, -1);
	operation = `add;
    }
    /* dev!="" -> Edit */
    else {
	string typ = device_type(dev);
	integer num = device_num(dev);
	string n = sformat("%1",num);

	map typemap = lookup(CONFIG, typ, $[]);
	if(!haskey(typemap, n)) {
	    y2error("Key not found: %1", dev);
	    return false;
	}
	devmap = lookup(typemap, n, $[]);

	type = typ;
	device = num;
	operation = `edit;
    }

    /* general stuff */
    name = devmap["NAME"]:"";
    unique = devmap["UNIQUE"]:"";

    /* modem settings */
    Init1 = devmap["INIT1"]:"ATZ";
    Init2 = devmap["INIT2"]:"AT Q0 V1 E1 S0=0 &C1 &D2 +FCLASS=0";
    Init3 = devmap["INIT3"]:"";
    BaudRate = tointeger(devmap["SPEED"]:"57600");

    PulseDial = devmap["DIALCOMMAND"]:"ATDT" == "ATDP";
    Speaker = devmap["INIT8"]:"ATM1" == "ATM1";
    Carrier = devmap["INIT9"]:"" == "";

    Device = devmap["MODEM_DEVICE"]:"/dev/modem";
    DialPrefix = devmap["DIALPREFIX"]:"";
    PPPDoptions = devmap["PPPD_OPTIONS"]:"";

    /* provider settings */
    Providers::provShortName = devmap["PROVIDER"]:"";

    //IsDefault = true;
    //DefaultModem = "";
    //Provider = devmap["PROVIDER"]:"";

    return true;
}

/**
 * Delete a modem (prepare before Commit)
 * @return true if success
 */
global define boolean Delete(string dev) ``{
    operation = nil;
    string typ = device_type(dev);
    integer num = device_num(dev);

    map typemap = lookup(CONFIG, typ, $[]);
    if(!haskey(typemap, sformat("%1",num))) {
        y2error("Key not found: %1", dev);
        return false;
    }

    type = typ;
    device = num;
    operation = `delete;
    return true;
}

/**
 * Commit the pending operation
 * @return true if success
 */
global define boolean Commit() ``{
    y2debug("Commit(%1)",operation);
    if(operation == `add || operation == `edit) {
	map newdev = $[
	    "STARTMODE"	: "manual",
	    "BOOTPROTO"	: "none",
	    "UNIQUE"	: unique,
	    "INIT1"	: Init1,
	    "INIT2"	: Init2,
	    "SPEED"	: BaudRate,
	    "INIT8"	: Speaker ? "ATM1" : "ATM0",
	    "INIT9"	: Carrier ? "" : "ATX3",
	    "DIALCOMMAND"	: PulseDial ? "ATDP" : "ATDT",
	    "MODEM_DEVICE"	: Device,
	    "DIALPREFIX"	: DialPrefix,
	    "PROVIDER"		: Providers::provShortName,
	    "PROVIDER_NAME"	: Providers::provLongName,
	    "PPPD_OPTIONS"	: PPPDoptions,
	];

	if(operation == `add)
	    ConfigAdd(type, device, newdev, true);
	else
	    ConfigAdd(type, device, newdev, false);
    }
    else if(operation == `delete) {
	ConfigDelete(type, device);
	change(DELETED, type+device);
    }
    else {
	y2error("Unknown operation: %1", operation);
	return false;
    }

    modified = true;
    operation = nil;
    return true;
}

/**
 */
global define boolean Import(map settings) ``{
    Providers::Import(lookup(settings, "_PROVIDERS", $[]));
    map blah = remove(settings, "_PROVIDERS");
    CONFIG = eval(blah);
}

/**
 */
global define map Export() ``{
    map PROVIDERS = Providers::Export();
    map blah = add(CONFIG, "_PROVIDERS", PROVIDERS);
    return eval(blah);
}

/**
 * Build a textual summary and a list of unconfigured cards
 * @return summary of the current configuration
 */
global define list Summary(boolean split) ``{
    return BuildSummary(split);
}

/**
 */
global define list Overview() ``{
    return BuildOverview();
}

/**
 */
define boolean Abort() ``{
    if(AbortFunction != nil)
        return eval(AbortFunction) == true;
    return false;
}

/**
 */
global define void Select(integer which) ``{
    SelectHardware(which);
}

/* EOF */
}
