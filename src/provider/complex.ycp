/**
 * File:	include/network/provider/complex.ycp
 * Package:	Configuration of network
 * Summary:	Complex providers dialogs
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "network";

import "Providers";

import "Wizard";
import "Wizard_hw";

include "ui/common_popups.ycp";
include "ui/common_messages.ycp";

include "network/routines.ycp";
include "network/complex.ycp";

/**
 * Return a modification status
 * @return true if data was modified
 */
global define boolean Modified() ``{
    return Providers::Modified();
}

/**
 * Commit changes to internal structures
 * @return always `next
 */
global define symbol Commit() ``{
    Providers::Commit();
    return `next;
}

/**
 * Read settings dialog
 * @return `abort if aborted and `next otherwise
 */
global define symbol ReadDialog() ``{

    /* Providers read dialog help 1/2 */
    string help = _("<p><b><big>Initializing provider
configuration</big></b><br>Please wait...<br></p>") +

    /* Providers read dialog help 2/2 */
_("<p><b><big>Aborting the initialization:</big></b><br>
Safely abort the configuration utility by pressing <B>Abort</B> now.</p>");

    Wizard::RestoreHelp(help);
    Providers::AbortFunction = ``{return PollAbort();};
    boolean ret = Providers::Read("all");
    return ret ? `next : `abort;
}

/**
 * Write settings dialog
 * @return `abort if aborted and `next otherwise
 */
global define symbol WriteDialog() ``{
    if(!Modified()) return `next;

    /* Providers write dialog help 1/2 */
    string help = _("<p><b><big>Saving provider
configuration</big></b><br>Please wait...<br></p>") +

    /* Providers write dialog help 2/2 */
_("<p><b><big>Aborting saving:</big></b><br>
Abort saving by pressing <b>Abort</b>.</p>");

    Wizard::RestoreHelp(help);
    Providers::AbortFunction = ``{return PollAbort() && ReallyAbort();};
    boolean ret = Providers::Write();
    return ret ? `next : `abort;
}

/**
 * Main dialog
 * @return dialog result
 */
global define symbol SummaryDialog() ``{

    /* Providers summary dialog caption */
    string caption = _("Providers configuration");

    /* Providers summary dialog help 1/3 */
    string help = _("<p><b><big>Provider setup</big></b><br>
Configure your provider here.<br></p>") +

    /* Providers summary dialog help 2/3 */
_("<p><b><big>Adding a provider:</big></b><br>
Choose a provider from the list of available providers
then press <b>Configure</b>.</p>") +

    /* Providers summary dialog help 3/3 */
_("<p><b><big>Editing or deleting:</big></b><br>
If you press <b>Edit</b>, an additional dialog in which
to change the configuration opens.</p>");

    list summary = Providers::Summary(true);
    list unconfigured = select(summary, 1, []);
    string configured = select(summary, 0, "");
    y2debug("unconfigured=%1", unconfigured);

    /* Frame label */
    term contents = Wizard_hw::DetectedContent(_("Providers to configure"),
	    unconfigured, false, configured);

    Wizard::SetContentsButtons(caption, contents, help,
	    BackButtonLabel(), FinishButtonLabel());

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
	/* overview dialog */
	else if(ret == `edit_button) {
	    ret = `overview;
	    break;
	}
	/* configure the selected card */
	else if(ret == `configure_button) {
	    Providers::AddEdit("", false);
	    any selected = UI::QueryWidget(`id(`detected_selbox), `CurrentItem);
	    if(selected == `other) {
		ret = `other;
	    }
	    else {
		Providers::Select(selected);
		ret = `configure;
	    }
	    break;
	}
	else if(ret == `next || ret == `back) {
	    break;
	}
	else {
	    y2error("unexpected retcode: %1", ret);
	    continue;
	}
    }

    return ret;
}

/**
 * Overview dialog
 * @return dialog result
 */
global define symbol OverviewDialog() ``{

    /* Providers overview dialog help caption */
    string caption = _("Providers configuration overview");

    /* Providers overview dialog help 1/3 */
    string help = _("<p><b><big>Providers overview</big></b><br>
Obtain an overview of installed providers. Additionally,
edit their configuration.<br></p>") +

    /* Providers overview dialog help 2/3 */
_("<p><b><big>Adding a provider:</big></b><br>
Press <b>Add</b> to configure a new provider manually.</p>") +

    /* Providers overview dialog help 3/3 */
_("<p><b><big>Editing or deleting:</big></b><br>
Choose a provider to change or remove.
Then press <b>Edit</b> or <b>Delete</b> as desired.</p>");

    list overview = Providers::Overview();
    y2debug("overview=%1",overview);

    term contents = OverviewTable(
	/* Table header */
	`header(_("Name"), _("Provider")),
	overview
    );

    Wizard::SetContentsButtons(caption, contents, help,
	    BackButtonLabel(), FinishButtonLabel());

    if(size(overview) < 1) {
	UI::ChangeWidget(`id(`edit), `Enabled, false);
	UI::ChangeWidget(`id(`delete), `Enabled, false);
    }

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
	/* add */
	else if(ret == `add) {
	    Providers::AddEdit("", false);
	    break;
	}
	/* edit */
	else if(ret == `edit) {
	    string dev = UI::QueryWidget(`id(`table), `CurrentItem);
	    Providers::AddEdit(dev, false);
	    break;
	}
	/* delete */
	else if(ret == `delete) {
	    string dev = UI::QueryWidget(`id(`table), `CurrentItem);
	    Providers::Delete(dev);
	    Providers::Commit();
	    overview = Providers::Overview();
	    UI::ChangeWidget(`id(`table), `Items, overview);
	    if(size(overview) < 1) {
		UI::ChangeWidget(`id(`edit), `Enabled, false);
		UI::ChangeWidget(`id(`delete), `Enabled, false);
	    }
	    y2debug("overview=%1",overview);
	    continue;
	}
	else if(ret == `next || ret == `back) {
	    break;
	}
	else {
	    y2error("unexpected retcode: %1", ret);
	    continue;
	}
    }

    return ret;
}

if(false) {
Wizard::CreateDialog();
Providers::Read();
OverviewDialog();
}

/* EOF */
}
