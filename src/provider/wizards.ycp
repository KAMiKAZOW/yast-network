/**
 * File:	include/network/provider/wizards.ycp
 * Package:	Configuration of network
 * Summary:	Providers configuration wizards
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * Wizards definitions.
 */

{

textdomain "network";

import "Wizard";

include "wizard/sequencer.ycp";

// include "network/provider/details.ycp";
include "network/provider/dialogs.ycp";
include "network/provider/connection.ycp";
include "network/provider/complex.ycp";

/**
 * Whole configuration of modems
 * @return sequence result
 */
global define symbol ProvidersSequence() ``{

    /* popup text */
    string finished = _("Configure mail now?");

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ],
	"finish": [ ``( Finished(Providers::Modified(), finished, "mail", ["dialup"]) ), true ],
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: "finish"
	],
	"finish" : $[
	    `next	: `next,
	]
    ];

    string caption = _("Provider configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of modems but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol ProvidersAutoSequence() ``{
    string caption = _("Provider configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    symbol ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the modem configuration
 * @return sequence result
 */
global define symbol MainSequence() ``{

    map aliases = $[
	"summary"	: ``(SummaryDialog()),
	"overview"	: ``(OverviewDialog()),
	"configure"	: [ ``(OneProviderSequence("modem",true)), true ],
	"other"		: [ ``(OneProviderSequence("modem",true)), true ],
	"add"		: [ ``(OneProviderSequence("modem",true)), true ],
	"edit"		: [ ``(OneProviderSequence("modem",false)), true ],
    ];

    map sequence = $[
	"ws_start" : "summary",
	"summary" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `configure	: "configure",
	    `other	: "other",
	    `overview	: "overview"
	],
	"overview" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add",
	    `edit	: "edit",
	],
	"other" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"configure" : $[
	    `abort	: `abort,
	    `next	: "summary",
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/**
 * Workflow of the configuration of one modem
 * @param country true if country list should be shown
 * @return sequence result
 */
global define symbol OneProviderSequence(string type, boolean country) ``{

    map aliases = $[
	"providers"		: ``(ProvidersDialog(type)),
	"provider"		: ``(ProviderDialog(type)),
	"new_provider"		: ``(ProviderDialog(type)),
	"connection"		: ``(ConnectionDialog(type)),
//	"ipdetails"		: ``(IPDetailsDialog(type)),
	"commit"		: [ ``(Commit()), true ],
    ];

    string entry = "provider";
    if(country) entry = "providers";

    map sequence = $[
	"ws_start" : entry,
	"providers" : $[
	    `next   : "provider",
	    `new    : "new_provider",
	    `abort  : `abort
	],
	"provider" : $[
	    `next   : "connection",
	    `abort  : `abort
	],
	"new_provider" : $[
	    `next   : "connection",
	    `abort  : `abort
	],
	"connection" : $[
//	    `next   : "ipdetails",
	    `next   : "commit",
	    `abort  : `abort
	],
/*
	"ipdetails" : $[
	    `next   : "commit",
	    `abort  : `abort
	],
*/
	"commit" : $[
	    `next   : `next,
	],
    ];

    return WizardSequencer(aliases, sequence);
}

/* EOF */
}
